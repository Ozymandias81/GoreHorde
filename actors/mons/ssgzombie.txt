ACTOR SSGZombie 30137
{
//$Category Realm667 Monsters
Tag "SSG Zombie"
Game Doom
Translation "112:127=16:47"
spawnid 144
gibhealth 35
Health 120
Radius 20
Height 56
Scale 0.9
Speed 9
PainChance 170
Decal Bulletchip
MONSTER
+FLOORCLIP
SEESOUND "SSGUNER/sight"
ATTACKSOUND "SSGUNER/SSG"
PAINSOUND "grunt/pain"
DEATHSOUND "SSGUNER/death"
ACTIVESOUND "SSGUNER/idle"
OBITUARY "$SSGZOMBO"
DropItem "SuperShotgun", 96
DropItem "ShellBox", 64
MaxStepHeight 32
MaxDropOffHeight 32
states
	{
	Spawn:
	GPOS A 10 A_Look
	Loop
	See:
	GPOS AABBCCDD 4 A_Chase
	Loop
	Missile:
	GPOS E 10 BRIGHT A_FaceTarget
	GPOS F 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), -2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
	GPOS F 0 A_SpawnItemEx("ShotCaseSpawn", random(20,24), -4, random(24,28), 0, 0, 0, 0, random(128,132))
	GPOS F 8 BRIGHT A_CustomBulletAttack(11.2, 7.1, 20, 3, "Bulletpuff")
	TNT1 A 0 A_JumpIf(waterlevel > 0,3)
	TNT1 AA 0 A_SpawnItemEx("ShotSmokeSpawner",random(8,10),10,28,momx,momy,random(1,3),0,0,0)
	GPOS E 8 BRIGHT
	Goto See
	Pain:
	GPOS G 3
	GPOS G 3 A_Pain
	Goto See
	Death:
	GPOS H 7
	GPOS I 7 A_Scream
	GPOS J 7 A_NoBlocking
	GPOS K 7
	GPOS L 7
	GPOS M 7
	GPOS N -1
	Stop
	XDeath: //new
	GPOS O 0 A_SpawnDebris("NashGore_Gib9",0)
	GPOS O 0 A_SpawnDebris("NashGore_Gib10",0)
	GPOS O 0 A_SpawnDebris("NashGore_Gib11",0)
	GPOS O 0 A_SpawnDebris("NashGore_Gib11",0)
	GPOS O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	GPOS O 5
	GPOS P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	GPOS P 5 A_XScream
	GPOS Q 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
	GPOS Q 5 A_NoBlocking
	GPOS R 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
	GPOS RS 5
	GPOS T -1
	Stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
	stop
	Raise:
	GPOS NMLKJIH 5
	Goto See
	}
}