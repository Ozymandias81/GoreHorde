Actor Vore 30118
{
//$Category Realm667 Monsters
Tag "Vore"
Game Doom
Translation "16:47=[205,0,215]:[40,0,96]","168:191=[205,0,215]:[40,0,96]"
spawnid 149
Health 600
Speed 4
Radius 30
Height 56
Mass 1000
PainChance 28
RadiusDamageFactor 0.5
Monster
+FLOORCLIP
+NOTARGET
Obituary "%o couldn't escape a Vore"
SeeSound "Vore/Sight"
ActiveSound "Vore/Active"
PainSound "Vore/Pain"
DeathSound "Vore/Death"
DropItem "BackPack", 96
DropItem "MediumPotionsSpawner", 16
BloodColor "Purple"
MaxStepHeight 40
MaxDropOffHeight 48
States
	{
	Spawn:
	VORE D 10 A_Look
	Loop
	See:
	VORE AA 3 A_Chase
	VORE B 7 A_Chase
	VORE CC 3 A_Chase
	VORE D 7 A_Chase
	VORE EE 3 A_Chase
	VORE F 7 A_Chase
	Loop
	Melee:
	Missile:
	VORE G 0 Bright A_PlaySoundEx("Vore/Attack", "Body")
	VORE GHHHH 4 Bright A_FaceTarget
	VORE II 4 Bright A_FaceTarget
	VORE J 8 Bright A_CustomMissile("VoreFirepod", 32, 0, 0)
	VORE JJ 4 Bright A_FaceTarget
	Goto See
	Pain:
	VORE K 4
	VORE K 4 A_Pain
	Goto See
	Death:
	VORE L 6
	VORE M 6 A_Scream
	VORE N 6
	VORE O 6 A_NoBlocking
	VORE P 8
	VORE Q -1
	XDeath:	//new
	VORE L 0 A_SpawnDebris("Gore_PurpleGib11",0)
	VORE L 0 A_SpawnDebris("Gore_PurpleGib11",0)
	VORE L 0 A_SpawnItem("Gore_GibGeneratorPurple",0,0,0,0)
	VORE L 6
	VORE M 0 A_SpawnItem("Gore_GibGeneratorPurple",0,0,0,2)
	VORE M 6 A_Scream
	VORE N 0 A_SpawnItem("Gore_GibGeneratorPurple",0,0,0,4)
	VORE N 6
	VORE O 0 A_SpawnItem("Gore_GibGeneratorPurple",0,0,0,6)
	VORE O 6 A_NoBlocking
	VORE P 8
	VORE Q -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumPurple",0,0,0,0)
	stop
	Raise:
	VORE QPONML 6
	Goto See
	}
}

Actor VoreFirepod
{
Radius 6
Height 8
Speed 12
FastSpeed 18
Damage (Random(10,30))
DamageType Fire
Projectile
+SEEKERMISSILE
RenderStyle Add
Alpha 0.9
SeeSound "Vore/Throw"
DeathSound "Vore/Hit"
Decal RevenantScorch
States
	{
	Spawn:
	VOBA A 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA A 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA B 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA B 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA B 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA B 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA C 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA C 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA C 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA C 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA C 0 Bright A_JumpIfTargetInLOS(1, 360, 1)
	Loop
	VOBA A 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA A 0 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA A 1 Bright A_SpawnItemEx("VoreFirepodTrail", random(5, -5), 0, random(5, -1), 0, 0, 0, 180)
	VOBA C 0 Bright A_JumpIfTargetInLOS(1, 360, 1)
	Goto Spawn+4
	VOBA BBB 0 Bright A_SeekerMissile(90, 90, 2)
	Goto Spawn+4
	Death:
	VOBA D 0 Bright A_Explode(30, 90)
	VOBA DEFGHI 4 Bright
	Stop
	}
}

Actor VoreFirepodTrail
{
Radius 1
Height 1
Speed 0
Projectile
RenderStyle Add
Alpha 0.9
+NOCLIP
States
	{
	Spawn:
	Death:
	TNT1 A 3
	VOBT ABCDE 3 Bright
	Stop
	}
}