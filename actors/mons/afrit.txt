Actor Afrit 30111
{
//$Category Realm667 Monsters
Tag "Afrit"
Game Doom
spawnid 100
Health 800
Speed 6
Radius 24
Height 72
PainChance 50
Mass 500
Meleedamage 10
SeeSound "Baron/Sight"
PainSound "Baron/Pain"
DeathSound "Baron/Death"
ActiveSound "Baron/Active"
MeleeSound "Baron/Melee"
DropItem "Gas", 48
ReactionTime 8
MONSTER
+NOGRAVITY
+NOBLOOD
+FLOAT
+DONTHURTSPECIES
Obituary "%o was scorched by an Afrit"
HitObituary "%o found the Afrit too hot to handle."
DamageFactor Fire, 0
DamageFactor Ice, 1.1
Damagefactor "HellFire", 0
BloodColor "darkorange1"
States
	{
	Spawn:
	FRIT A 0
	FRIT A 0 A_CustomMissile("OrbitComet", 44, 0, 0, 2, 0)
	FRIT A 0 A_CustomMissile("OrbitComet2", 44, 0, 180, 2, 0)
	FRIT AABB 4 Bright A_Look
	Goto Spawn+3
	Idle:
	FRIT ABCD 4 Bright A_Look
	Loop
	See:
	FRIT AABBCCDD 2 Bright A_Chase
	Loop
	Melee:
	FRIT S 0 A_Jump(64, "Melee2")
	FRIT ST 6 Bright A_FaceTarget
	FRIT U 6 Bright A_CustomComboAttack("AfritBall", 44, (10*Random(1, 8)), "Baron/Melee")
	Goto See
	Melee2:
	FRIT E 6 Bright A_FaceTarget
	FRIT F 6 Bright A_CustomMissile("HellFire", 44, 0, 0)
	FRIT G 6 Bright A_FaceTarget
	Goto See
	Missile:
	FRIT S 0 A_Jump(64, "Missile3")
	FRIT S 0 A_Jump(128, "Missile2")
	FRIT ST 6 Bright A_FaceTarget
	FRIT U 6 Bright A_CustomComboAttack("AfritBall", 44, (10*Random(1, 8)), "Baron/Melee")
	Goto See
	Missile2:
	FRIT EF 6 Bright A_FaceTarget
	FRIT G 6 Bright A_CustomMissile("Comet", 44, 0, 0)
	Goto See
	Missile3:
	FRIT E 6 Bright A_FaceTarget
	FRIT F 6 Bright A_CustomMissile("Hellfire", 44)
	FRIT G 6 Bright A_FaceTarget
	Goto See
	Pain:
	FRIT H 4 Bright
	FRIT H 4 Bright A_Pain
	Goto See
	Death:
	FRIT I 0 Bright A_NoGravity
	FRIT I 6 Bright A_Scream
	FRIT J 5 Bright A_NoBlocking
	FRIT KLMNOPQR 4 Bright
	Stop
	}
}

Actor AfritBall
{
Radius 6
Height 8
Speed 15
Damage 8
Projectile
RenderStyle Add
Alpha 0.8
SeeSound "Imp/Attack"
DeathSound "Imp/ShotX"
Decal BaronScorch
States
	{
	Spawn:
	FRTM AB 5 Bright
	Loop
	Death:
	FRTM CDE 6 Bright
	Stop
	}
}

Actor Comet
{
Radius 6
Height 8
Speed 10
Damage 5
Scale 0.55
Projectile
SeeSound "Afrit/CometFire"
DeathSound "Afrit/CometHit"
Decal Scorch
States
	{
	Spawn:
	COMT AAAABBBBCCCC 1 Bright A_SpawnItemEx("CometTail", 0, 0, 0, 0, 0, 0, 0, 128)
	Loop
	Death:
	COMT D 3 Bright A_SpawnItemEx("CometDeathGlow", 0, 0, 0, 0, 0, 0, 0, 128)
	COMT E 3 Bright A_Explode(80, 80)
	COMT E 0 A_SpawnItemEx("CometDeath", 0, 0, 0, 0, 0, 0, 0, 128)
	Stop
	}
}

Actor CometTail
{
Projectile
+NOCLIP
RenderStyle Add
Alpha 0.4
States
	{
	Spawn:
	FRTB ABCDEFGHI 1 Bright
	Stop
	}
}

Actor CometDeathGlow : CometTail
{
Scale 2.0
Alpha 0.6
States
	{
	Spawn:
	FRTB ABCDEFGHI 3 Bright
	Stop
	}
}

Actor CometDeath
{
Projectile
RenderStyle Add
+NOCLIP
Alpha 0.7
States
	{
	Spawn:
	COMT FGHI 3 Bright
	Stop
	}
}

Actor OrbitComet : Comet
{
SeeSound ""
+NOCLIP
Scale 0.28
Speed 5
States
	{
	Spawn:
	COMT AABBCC 1 Bright A_SpawnItemEx("OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
	COMT A 0 A_CustomMissile("OrbitComet", 0, 0, 75)
	COMT A 0 A_SpawnItemEx("OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
	Stop
	}
}

Actor OrbitComet2 : OrbitComet
{
States
	{
	Spawn:
	COMT AABBCC 1 Bright A_SpawnItemEx("OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
	COMT A 0 A_CustomMissile("OrbitComet2", 0, 0, -75)
	COMT A 0 A_SpawnItemEx("OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
	Stop
	}
}

Actor OrbitCometTail : CometTail {Scale 0.5}

Actor HellFire
{
+NOBLOCKMAP
+MISSILE
+DROPOFF
Damage 0
States
	{
	Spawn:
	TNT1 A 1
	Loop
	Death:
	TNT1 A 0 Bright A_CustomMissile("HellFire1", 0, 0, -180)
	TNT1 A 0 Bright A_CustomMissile("HellFire1", 0, 16, -175)
	TNT1 A 0 Bright A_CustomMissile("HellFire1", 0, 32, -170)
	TNT1 A 0 Bright A_CustomMissile("HellFire1", 0, -16, -185)
	TNT1 A 0 Bright A_CustomMissile("HellFire1", 0, -32, -190)
	TNT1 A 10 Bright A_PlaySound("Afrit/Hellfire")
	Stop
	}
}

Actor HellFire1
{
+NOCLIP
+MISSILE
+DROPOFF
Speed 2
Damage 2
States
	{
	Spawn:
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 0, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, 16, 0)
	TNT1 A 10 A_CustomMissile("HellFire2", 0, -16, 0)
	Stop
	}
}

Actor HellFire2
{
+NOCLIP
+DROPOFF
+RANDOMIZE
Scale 1.5
Speed 1
Damage 2
RenderStyle Add
Alpha 0.67
Damagetype "HellFire"
Obituary "%o was scorched by an Afrit"
States
	{
	Spawn:
	TNT1 A 8
	FRTF A 3 Bright
	FRTF A 0 A_Explode(3, 64)
	FRTF B 3 Bright
	FRTF B 0 A_Explode(3, 64)
	FRTF C 3 Bright
	FRTF C 0 A_Explode(3, 64)
	FRTF C 0
	FRTF C 0 A_Jump(191, 4)
	FRTF C 0 A_CustomMissile("HellFire2", 0, 24, 0)
	FRTF C 0 A_CustomMissile("HellFire2", 0, 0, 0)
	FRTF C 0 A_CustomMissile("HellFire2", 0, -24, 0)
	FRTF DEFGHIJKLMNO 3 Bright
	Stop
	}
}