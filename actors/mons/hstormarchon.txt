ACTOR HellstormArchon : BaronOfHell 30106 //Some sprites & actors renamed due to prevent connections with Belphegor & ArchonOfHell
{
//$Category Realm667 Monsters
Tag "Hellstorm Archon"
spawnid 117
gibhealth 35
Health 2000
Radius 24
Height 64
Mass 1000
Speed 8
PainChance 20
MONSTER
MeleeDamage 10
Obituary "%o was no match for an hellstorm archon."
HitObituary "%o was ripped apart by an hellstorm archon."
+NOTARGET
+FLOORCLIP
+MISSILEMORE
+NORADIUSDMG
+BOSS
+DONTHURTSPECIES
+NOFEAR
SeeSound "monster/arcsit"
PainSound "baron/pain"
DeathSound "monster/arcdth"
MeleeSound "baron/melee"
DropItem "DarkMana", 64
BloodColor "0 a3 0"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	HSA3 AB 10 A_Look
	Loop
	See:
	HSA3 AABBCCDD 3 A_Chase
	Loop
	Missile:
	HSA3 B 0 A_Jump(128,13)
	HSA3 EF 8 A_FaceTarget
	HSA3 G 0 A_CustomMissile ("BaronBall",28,0,0,0)
	HSA3 G 0 A_CustomMissile ("BaronBall",28,0,-4,0)
	HSA3 G 6 A_CustomMissile ("BaronBall",28,0,4,0)
	HSA3 E 0 A_Jump(64,7)
	HSA3 ST 8 A_FaceTarget
	HSA3 U 0 A_CustomMissile ("BaronBall",28,0,0,0)
	HSA3 U 0 A_CustomMissile ("BaronBall",28,0,-4,0)
	HSA3 U 6 A_CustomMissile ("BaronBall",28,0,4,0)
	HSA3 S 0 A_Jump(64,1)
	Goto See
	HSA3 HI 8 A_FaceTarget
	HSA3 S 0 A_Jump(128,2)
	HSA3 J 8 A_CustomMissile ("HArchonComet",28,0,0,0)
	Goto See
	HSA3 J 0 A_CustomMissile ("BaronBall",28,0,-6,0)
	HSA3 J 0 A_CustomMissile ("BaronBall",28,0,-4,0)
	HSA3 J 0 A_CustomMissile ("BaronBall",28,0,-2,0)
	HSA3 J 0 A_CustomMissile ("BaronBall",28,0,2,0)
	HSA3 J 0 A_CustomMissile ("BaronBall",28,0,4,0)
	HSA3 J 6 A_CustomMissile ("BaronBall",28,0,6,0)
	Goto See
	Melee:
	HSA3 EF 8 A_FaceTarget
	HSA3 G 6 A_MeleeAttack
	HSA3 G 0 A_Jump (128,1)
	Goto See
	HSA3 ST 8 A_FaceTarget
	HSA3 U 6 A_MeleeAttack
	HSA3 G 0 A_Jump (64,1)
	Goto See
	HSA3 HI 8 A_FaceTarget
	HSA3 J 0 A_MeleeAttack
	HSA3 J 6 A_MeleeAttack
	Goto See
	Pain:
	HSA3 K 2
	HSA3 K 2 A_Pain
	Goto See
	Death:
	HSA3 L 8
	HSA3 M 8 A_Scream
	HSA3 N 8
	HSA3 O 8 A_NoBlocking
	HSA3 PQ 8
	HSA3 R -1 A_Fall
	Stop
	XDeath:
	XARC A 0 A_SpawnDebris("Gore_GreenGib11",0)
	XARC A 0 A_SpawnDebris("Gore_GreenGib11",0)
	XARC A 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,0)
	XARC A 5
	XARC B 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,1)
	XARC B 5 A_XScream
	XARC C 0 A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,2)
	XARC C 5 A_NoBlocking
	XARC D 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,3)
	XARC DEFGH 5
	XARC I -1
	stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumGreen",0,0,0,0)
	stop
	Raise:
	HSA3 RQPONML 8
	Goto See
	}
}

ACTOR HArchonComet
{
Radius 8
Height 12
Speed 25
Damage 20
Scale 1.0
PROJECTILE
RENDERSTYLE Normal
+THRUGHOST
SeeSound "weapons/firbfi"
DeathSound "weapons/hellex"
DONTHURTSHOOTER
+FIREDAMAGE
States
	{
	Spawn:
	ARCB AAAABBBBCCCC 1 Bright A_SpawnItem("HArchonCometTrail",0,0)
	loop
	Death:
	ARCB J 0 Bright A_SetTranslucent (0.67,1)
	ARCB J 3 Bright
	ARCB K 3 Bright A_Explode(128,128,0)
	ARCB LMN 3 Bright
	stop
	}
}

ACTOR HArchonCometTrail
{
Radius 3
Height 3
Scale 0.75
Speed 0
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.67
States
	{
	Spawn:
	NULL A 3 Bright
	ARCB DEFGHI 3 BRIGHT
	Stop
	}
}