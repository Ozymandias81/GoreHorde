actor StoneDemon 30117
{
//$Category Realm667 Monsters
Tag "Stone Demon"
Game Doom
spawnid 167
MONSTER
+FLOORCLIP
+NOBLOOD
+DROPOFF
+NORADIUSDMG
Scale 1.1
HitObituary "%o was smashed by a stone demon."
Health 400
PainChance 100
Speed 8
Radius 30
Height 56
Mass 9999999
MissileType StoneDemonBoulder
MissileHeight 24
DamageFactor Poison, 0
DamageFactor PoisonCloud, 0
SeeSound "stone/sight"
AttackSound "demon/melee"
PainSound "demon/pain"
DeathSound "stone/death"
ActiveSound "demon/active"
DropItem "GuardSphereST", 16
DropItem "ShellBox", 8
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	STON AB 10 A_Look
	Loop
	See:
	STON AABBCCDD 2 A_Chase
	Loop
	Missile:
	STON EF 6 A_FaceTarget
	STON E 6 A_MissileAttack
	Goto See
	Melee:
	STON EF 8 A_FaceTarget
	STON G 0 A_SargAttack
	STON G 6 A_SargAttack
	STON EF 8 A_FaceTarget
	STON G 6 A_SargAttack
	Goto See
	Pain:
	STON H 3
	STON H 3 A_Pain
	Goto See
	Death:
	STON IIIIIIIIII 0 A_CustomMissile("StoneDemonMiniBoulder",16,0,random(0,359),2,random(0,40))
	STON I 5
	STON J 5 A_Scream
	STON K 5
	STON L 5 A_NoBlocking
	STON MN 5
	STON OP 4
	STON Q -1
	Stop
	Raise:
	STON NMLKJI 5
	Goto See
	}
}

actor StoneDemonStatue : SwitchableDecoration 8190
{
//$Category New Obstacles
Tag "Stone Demon's Statue"
spawnid 142
+SOLID
Scale 1.1
Radius 30
Height 56
Mass 9999999
States
	{
	Spawn:
	Inactive:
	STON A 10
	Loop
	Active:
	TNT1 A 1 A_SpawnItemEx("StoneDemon",0,0,0,0,0,0,0,32)
	Stop
	}
}

actor StoneDemonBoulder
{
PROJECTILE
-NOGRAVITY
+DOOMBOUNCE
+BOUNCEONACTORS
SeeSound "stone/bounce"
Radius 6
Height 6
Scale 0.7
Speed 20
VSpeed 3
Damage 5
States
	{
	Spawn:
	STNB ABCDEFGH 3
	Loop
	Death:
	STNB H 200
	Stop
	}
}

actor StoneDemonMiniBoulder : StoneDemonBoulder
{
Damage 1
Scale 0.35
SeeSound ""
}