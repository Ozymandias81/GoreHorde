ACTOR ESoul 25628// some actors renamed due to prevent connections with Shadow
{
//$Category Realm667 Monsters
Tag "Ethereal Soul"
Game Doom
spawnid 157
obituary "%o was raped by a black soul."
health 180
radius 16
height 32
mass 400
speed 5
damage 5
painchance 128
renderstyle Add
Alpha 0.4
attacksound "skull/melee"
PainSound "nightmare/pain"
DeathSound "nightmare/death"
ActiveSound "nightmare/active"
MONSTER
+FLOAT
+NOGRAVITY
+NOICEDEATH
+MISSILEMORE
+DONTFALL
+STEALTH
+NOBLOOD
+NOBLOODDECALS
+DONTGIB
states
	{
	Spawn:
	ETHS ABCD 10 bright A_Look
	loop
	See:
	ETHS AABBCCDD 3 bright A_Chase
	loop
	Missile:
	ETHS C 0 bright A_Jump(128,9)
	ETHS C 6 bright A_FaceTarget
	ETHS E 4 bright A_SkullAttack
	ETHS E 0 bright A_FaceTarget
	ETHS F 4 bright A_SkullAttack
	ETHS F 0 bright A_FaceTarget
	ETHS E 4 bright A_SkullAttack
	ETHS E 0 bright A_FaceTarget
	ETHS F 4 bright A_SkullAttack
	goto See
	ETHS P 3 bright A_FaceTarget
	ETHS Q 4 bright A_FaceTarget
	ETHS R 5 bright A_CustomMissile("EShadowBall", 0, 0, 0)
	ETHS E 4 bright
	goto See
	Pain:
	ETHS G 3 bright
	ETHS G 3 bright A_Pain
	goto See
	Death:
	ETHS F 3 bright
	ETHS H 3 bright A_ScreamAndUnblock
	ETHS IJKLMNO 3 bright
	stop
	}
}

ACTOR EShadowBall
{
Radius 6
Height 8
Speed 12
Damage 4
Projectile
+RANDOMIZE
RenderStyle "ADD"
Alpha 0.75
SeeSound "imp/attack"
DeathSound "imp/shotx"
Decal DoomImpScorch
States
	{
	Spawn:
	SBAL ABC 4 BRIGHT A_SpawnItemEx("EShadowTrail", 0, 0, 0, 0, 0, 0, 0, 128)
	Loop
	Death:
	SBAL C 5 Bright
	SBAL DEFGH 4 BRIGHT
	Stop
	}
}

actor EShadowTrail
{
Radius 1
Height 1
Speed 0
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.6
+NOCLIP
States
	{
	Spawn:
	SHTR ABCDEF 4 Bright
	Stop
	}
}