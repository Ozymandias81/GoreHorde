ACTOR HellWarrior 3119
{
//$Category Realm667 Monsters
Tag "Hell Warrior"
Game Doom
translation "168:191=210:224"
spawnid 118
gibhealth 35
Health 400
Speed 8
Radius 24
Height 64
PainChance 50
Mass 1000
Meleedamage 8
SeeSound "knight/sight"
PainSound "knight/pain"
DeathSound "knight/death"
ActiveSound "knight/active"
MeleeSound "baron/melee"
REACTIONTIME 8
MONSTER
+FLOORCLIP
Missiletype HellWarriorBall
Obituary "%o was tagged by the hell warrior's fireball."
HitObituary "%o was torn apart by a hell warrior."
DropItem "MediumPotionsSpawner", 32
DropItem "ShellBox", 64
BloodColor "darkorange1"
MaxStepHeight 32
MaxDropOffHeight 32
	States
	{
	Spawn:
	HWAR AB 10 A_Look
	Loop
	See:
	HWAR AABBCCDD 3 A_Chase
	Loop
	Missile:
	HWAR E 0 A_Jump(128, 4)
	HWAR EF 8 A_FaceTarget
	HWAR G 8 A_ComboAttack
	Goto See
	HWAR H 0 A_Jump(128, 6)
	HWAR H 0 A_SetInvulnerable
	HWAR H 6 A_FaceTarget
	HWAR I 4 BRIGHT A_Custommissile("HellWarriorBall2", 25, 0, 0)
	HWAR H 6 A_FaceTarget
	HWAR H 0 A_UnSetInvulnerable
	Goto See
	HWAR H 0 A_SetInvulnerable
	HWAR H 6 A_FaceTarget
	HWAR I 4 BRIGHT A_Custommissile("HellWarriorBall2", 25, 0, 0)
	HWAR H 16 A_SpidRefire
	HWAR I 4 BRIGHT A_Custommissile("HellWarriorBall2", 25, 0, 0)
	HWAR H 6 A_FaceTarget
	HWAR H 0 A_UnSetInvulnerable
	Goto See
	Melee:
	HWAR EF 8 A_FaceTarget
	HWAR G 8 A_ComboAttack
	Goto See
	Pain:
	HWAR J 6 A_Pain
	HWAR H 0 A_SetInvulnerable
	HWAR HHHHHHH 6 A_FaceTarget
	HWAR H 0 A_UnSetInvulnerable
	Goto See
	Death:
	HWAE K 0 A_FaceTarget
	HWAR K 5 A_SpawnItemEx("HellWarriorShield", 0, 0, 25, 6, 0, 0, 60, 128)
	HWAR L 5 A_Scream
	HWAR M 5
	HWAR N 5 A_NoBlocking
	HWAR OPQRS 5
	HWAR T -1
	XDeath:
	HWAE K 0 A_SpawnDebris("Gore_OrangeGib11",0)
	HWAE K 0 A_SpawnDebris("NashGore_Gib11",0)
	HWAE K 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,0)
	HWAE K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	HWAE K 0 A_FaceTarget
	HWAR K 5 A_SpawnItemEx("HellWarriorShield", 0, 0, 25, 6, 0, 0, 60, 128)
	HWAE L 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,1)
	HWAR L 5 A_Scream
	HWAE M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	HWAR M 5
	HWAE N 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,3)
	HWAR N 5 A_NoBlocking
	HWAE O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
	HWAR OPQRS 5
	HWAR T -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumOrange",0,0,0,0)
	stop
	Raise:
	HWAR QPONMLK 5
	Goto See
	}
}

actor HellWarriorBall
{
Radius 6
Height 8
Speed 14
Damage 8
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.7
SeeSound "imp/attack"
DeathSound "imp/shotx"
Decal BaronScorch
States
	{
	Spawn:
	HWFB AB 5 Bright
	Loop
	Death:
	HWFB CDE 5 Bright
	Stop
	}
}

actor HellWarriorBall2
{
Radius 6
Height 8
Speed 8
Damage 2
PROJECTILE
RENDERSTYLE ADD
+RANDOMIZE
+RIPPER
ALPHA 0.8
SCALE 0.4
SeeSound "imp/attack"
DeathSound "imp/shotx"
States
	{
	Spawn:
	GRBA ABCDEFGH 2 Bright
	Loop
	Death:
	GRBA AIJKLMN 3 Bright
	Stop
	}
}

ACTOR HellWarriorShield
{
Radius 8
Height 8
Speed 6
+DOOMBOUNCE
+DROPOFF
+Missile
States
	{
	Spawn:
	HWSH ABCDEFGH 3
	Loop
	Death:
	HWSH I -1
	Loop
	}
}