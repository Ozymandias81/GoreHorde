ACTOR PlasmaDemon 3124
{
//$Category Realm667 Monsters
translation "16:47=112:127","168:191=114:128"
Tag "Plasma Demon"
Game Doom
spawnid 132
gibhealth 35
Health 200
Speed 12
Height 56
Radius 30
Mass 400
PainChance 100
MeleeDamage 6
REACTIONTIME 8
MONSTER
+FLOORCLIP
+DONTHURTSPECIES
SeeSound "demon/sight"
PainSound "demon/pain"
DeathSound "demon/death"
ActiveSound "demon/active"
MeleeSound "demon/melee"
HitObituary "A plasma demon has something to eat tonight."
Obituary "%o was fried by a plasma demon."
MissileType PDemBall
DropItem "Cell", 32
DropItem "CellPack", 8
BloodColor "0 a3 0"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	DIMP AB 8 A_Look
	Loop
	See:
	DIMP AABBCCDD 2 A_Chase
	Loop
	Melee:
	DIMP EF 8 A_FaceTarget
	DIMP G 8 A_MeleeAttack
	Goto See
	Missile:
	DIMP P 10 A_FaceTarget
	DIMP Q 6 A_ComboAttack
	Goto See
	Pain:
	DIMP H 2
	DIMP H 2 A_Pain
	Goto See
	Death:
	DIMP I 6
	DIMP J 6 A_Scream
	DIMP K 4
	DIMP L 4 A_NoBlocking
	DIMP M 4
	DIMP N -1
	Stop
	XDeath: //new
	DIMP I 0 A_SpawnDebris("Gore_GreenGib11",0)
	DIMP I 0 A_SpawnItemEx("Gore_PDemonArm",10,0,32,0,random(2,8),0,0,random(0,180))
	DIMP I 0 A_SpawnDebris("Gore_GreenGib11",0)
	DIMP I 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,0)
	DIMP I 6
	DIMP J 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,2)
	DIMP J 6 A_Scream
	DIMP K 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,4)
	DIMP K 4
	DIMP L 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,6)
	DIMP L 4 A_NoBlocking
	DIMP M 4
	DIMP N -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumGreen",0,0,0,0)
	stop
	Raise:
	DIMP NMLKJI 4
	Goto See
	}
}

ACTOR PDemBall
{
Translation "64:79=112:127","208:239=114:128" //not perfect, but it works ;-)
Speed 20
Radius 6
Height 8
Damage 8
SeeSound "pd/attack" //connections with Imp removed
DeathSound "pd/shotx" //connections with Imp removed
PROJECTILE
RENDERSTYLE ADD
Decal RevenantScorch
States
	{
	Spawn:
	RELL ABCDEF 4 A_SpawnItemEx("PDemBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
	Loop
	Death:
	RELL GHI 4
	Stop
	}
}

ACTOR PDemBallTrail
{
Translation "64:79=112:127","208:239=114:128" //not perfect, but it works ;-)
Speed 0
Damage 0
Height 8
Radius 5
SeeSound "pd/attack" //WTF!?
DeathSound "pd/shotx" //WTF!?
PROJECTILE
RENDERSTYLE ADD
States
	{
	Spawn:
	FUCK ABCDE 4
	Stop
	}
}