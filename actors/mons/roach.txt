//The Roach is a fairly annoying monster that can regenerate 1 health every
//6 tics (almost 6 health per second, and can fully heal itself in under 30
//seconds).
//When it is under 40 health, it will switch on the Frightened flag and run
//away. When it hits 120 health, it'll turn the flag off and attack again.
//If the first argument is 1, it won't play a see sound.
//If the first argument is 2, it will alert other monsters in the vicinity
//when it sees a player.

Actor Roach 25629
{
//$Category Realm667 Monsters
Tag "Roach"
Game Doom
spawnid 159
Health 150
GibHealth 35
Radius 19
Height 48
Speed 8
PainChance 64
Mass 200
MONSTER
+DONTHURTSPECIES
SeeSound "Roach/Sight"
PainSound "Roach/Pain"
DeathSound "Roach/Death"
ActiveSound "Roach/Active"
MeleeDamage 4
DamageFactor PoisonCloud, 0
DamageFactor Poison, 0
Obituary "%o was scalded by a Roach"
DropItem "SmallPortablePotion", 64
DropItem "ShellBox", 32
BloodColor "0 a3 0"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	ROAC A 0
	ROAC A 0 A_JumpIf(Args[0] == 1, "Spawn2")
	ROAC A 0 A_JumpIf(Args[0] > 1, "Spawn3")
	ROAC AAAAAA 1 A_Look
	ROAC A 0 HealThing(2)
	Loop
	Spawn2:
	ROAC AAAAAA 1 A_LookEx(8, 0, 0, 0, 0, "See")
	ROAC A 0 HealThing(2)
	Loop
	Spawn3:
	ROAC AAAAAA 1 A_LookEx(0, 0, 0, 0, 0, "SeeAlert")
	ROAC A 0 HealThing(2)
	Loop
	SeeAlert:
	ROAC A 0 A_AlertMonsters
	Goto See
	See:
	ROAC B 0 A_JumpIfInventory("RoachScared", 1, 2)
	ROAC B 0 A_Jump(32, "Stand")
	ROAC B 0 A_JumpIfInventory("RoachScared", 1, 1)
	Goto See+4
	ROAC B 0 A_JumpIfHealthLower(120, 1)
	Goto HealthHigh
	ROAC B 0 A_JumpIfInventory("RoachScared", 1, 2)
	ROAC B 0 A_JumpIfHealthLower(40, "HealthLow")
	ROAC BB 2 A_Chase("Melee", "", 2)
	ROAC B 2 A_Chase
	ROAC C 0 HealThing(2)
	ROAC C 0 A_JumpIfInventory("RoachScared", 1, 1)
	Goto See+12
	ROAC C 0 A_JumpIfHealthLower(120, 1)
	Goto HealthHigh
	ROAC C 0 A_JumpIfInventory("RoachScared", 1, 2)
	ROAC C 0 A_JumpIfHealthLower(40, "HealthLow")
	ROAC CC 2 A_Chase("Melee", "", 2)
	ROAC C 2 A_Chase
	ROAC D 0 HealThing(2)
	ROAC D 0 A_JumpIfInventory("RoachScared", 1, 1)
	Goto See+20
	ROAC D 0 A_JumpIfHealthLower(120, 1)
	Goto HealthHigh
	ROAC D 0 A_JumpIfInventory("RoachScared", 1, 2)
	ROAC D 0 A_JumpIfHealthLower(40, "HealthLow")
	ROAC DD 2 A_Chase("Melee", "", 2)
	ROAC D 2 A_Chase
	ROAC E 0 HealThing(2)
	ROAC E 0 A_JumpIfInventory("RoachScared", 1, 1)
	Goto See+28
	ROAC E 0 A_JumpIfHealthLower(120, 1)
	Goto HealthHigh
	ROAC E 0 A_JumpIfInventory("RoachScared", 1, 2)
	ROAC E 0 A_JumpIfHealthLower(40, "HealthLow")
	ROAC EE 2 A_Chase("Melee", "", 2)
	ROAC E 2 A_Chase
	ROAC B 0 HealThing(2)
	Loop
	Stand:
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Chase("Melee", "Missile", 18)
	ROAC A 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC A 0 A_Jump(192, 1)
	Goto See+1
	ROAC A 0
	Loop
	HealthLow:
	ROAC A 0 A_GiveInventory("RoachScared", 1)
	ROAC A 0 A_ChangeFlag("Frightened", 1)
	Goto See+4
	HealthHigh:
	ROAC A 0 A_TakeInventory("RoachScared", 1)
	ROAC A 0 A_ChangeFlag("Frightened", 0)
	Goto See+4
	Melee:
	Missile:
	ROAC AA 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC AA 3 A_FaceTarget
	ROAC A 0 HealThing(2)
	ROAC FF 3 A_FaceTarget
	ROAC F 0 HealThing(2)
	ROAC G 0 A_CustomMissile("RoachBall", 32, -4, 1)
	ROAC G 6 A_CustomMissile("RoachBall", 32, 4, -1)
	ROAC G 0 HealThing(2)
	Goto See
	Pain:
	ROAC H 3
	ROAC H 3 A_Pain
	ROAC H 0 HealThing(2)
	Goto See
	Death:
	ROAC I 8 A_ScreamAndUnblock
	ROAC JKL 6
	ROAC M 0 A_Jump(18, 2)
	ROAC M -1
	Loop
	ROAC MMMMM 70
	ROAC MMMMM 70
	ROAC MMMMM 70
	ROAC M -1 Thing_Raise(0)
	Wait
	XDeath: //new
	ROAC I 0 A_SpawnDebris("Gore_GreenGib11",0)
	ROAC I 0 A_SpawnDebris("Gore_GreenGib11",0)
	ROAC I 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,0)
	ROAC I 4 A_XScream
	ROAC N 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,2)
	ROAC O 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,4)
	ROAC P 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,6)
	ROAC NOP 6
	ROAC Q 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,8)
	ROAC Q 6 A_NoBlocking
	ROAC RRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRR 0 A_SpawnItemEx ("GreenFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.5,0,160)
	ROAC RS 6
	ROAC T -1
	Stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RRRRRRRRRRRRRRRRRRRRRRRRRRRRRRRR 0 A_SpawnItemEx ("GreenFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.5,0,160)
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedSmallGreen",0,0,0,0)
	stop
	Raise:
	ROAC MLKJI 5
	Goto See
	}
}

Actor RoachScared : Inventory { Inventory.MaxAmount 1 }

Actor RoachBall : DoomImpBall
{
Damage (Random(5, 8))
Height 5
Radius 3
Scale 0.6
Speed 30
Decal PlasmaScorchLower
SeeSound "Roach/Fire"
DeathSound "Roach/Hit"
States
	{
	Spawn:
	RCHB AAAABBBB 1 Bright A_SpawnItemEx("RoachBallTrail", -(MomX/1.2), -(MomY/1.2), (MomZ/1.2*-1), (MomX/1.2), (MomY/1.2), (MomZ/1.2), -Angle, 138)
	Loop
	Death:
	RCHB C 0 Bright A_Explode(5, 32, 0)
	RCHB CDEFG 3 Bright
	Stop
	}
}

Actor RoachBallTrail
{
Projectile
RenderStyle Add
Alpha 0.5
Scale 0.6
States
	{
	Spawn:
	RCHB AABB 2 Bright A_FadeOut(0.05)
	Loop
	}
}