ACTOR ProfaneOne 2722 // : BaronOfHell removed for necro reasons
{
//$Category Realm667 Monsters
Tag "Profane One"
Game Doom
gibhealth 35
spawnid 133
Health 2000
Radius 24
Height 64
Mass 1000
Speed 10
PainChance 45
Monster
+FLOORCLIP
+BOSS
+MISSILEMORE
+MISSILEEVENMORE
+NOFEAR
Poisondamage 10
DamageFactor Poison, 0.5
DamageFactor PoisonCloud, 0.5
SeeSound "profane/sight"
PainSound "baron/pain"
DeathSound "profane/death"
ActiveSound "baron/active"
Obituary "%o is now the Profane One's trophy."
HitObituary "The Profane One mauled %o."
DropItem "DarkMana", 48
DropItem "RageSphere", 48
DropItem "ShellBox", 48
BloodColor "08 08 08"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	BOSX AB 10 A_Look
	Loop
	See:
	BOSX AABB 3 A_VileChase
	BOSX CCDD 3 A_VileChase
	Loop
	Melee:
	BOSX EF 8 A_FaceTarget
	BOSX G 8 A_CustomMeleeAttack (10, "profane/melee", Poison)
	Goto See
	Missile:
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_Jump(128,"Poison","Blood")
	Poison:
	BOSX EF 7 A_FaceTarget
	BOSX G 0 Bright A_CustomMissile ("PoisonBall",28,4,1,0)
	BOSX G 7 Bright A_CustomMissile ("PoisonBall",28,-4,-1,0)
	BOSX F 7 A_FaceTarget
	BOSX G 0 Bright A_CustomMissile ("PoisonBall",28,4,1,0)
	BOSX G 7 Bright A_CustomMissile ("PoisonBall",28,-4,-1,0)
	BOSX F 7 A_FaceTarget
	BOSX G 0 Bright A_CustomMissile ("PoisonBall",28,4,1,0)
	BOSX G 7 Bright A_CustomMissile ("PoisonBall",28,-4,-1,0)
	Goto See
	Blood:
	BOSX H 0 A_PlaySound("profane/vomit")
	BOSX H 15 A_FaceTarget
	BOSX H 0 A_CustomMissile ("BloodPuke", 60, 0, random(-10,10), 2, random(10,20))
	BOSX H 0 A_CustomMissile ("BloodPuke", 59, 0, random(-10,10), 2, random(10,20))
	BOSX H 0 A_CustomMissile ("BloodPuke", 58, 0, random(-10,10), 2, random(10,20))
	BOSX H 0 A_CustomMissile ("BloodPuke", 59, 0, random(-10,10), 2, random(10,20))
	BOSX H 0 A_CustomMissile ("BloodPuke", 60, 0, random(-10,10), 2, random(10,20))
	Goto See
	Pain:
	BOSX H 2
	BOSX H 2 A_Pain
	Goto See
	Heal:
	BOSX HH 10
	Goto See
	Death:
	BOSX I 8 A_Scream
	BOSX J 8 A_PlaySound("profane/die")
	BOSX K 8
	BOSX L 8 A_NoBlocking
	BOSX MN 8 A_Mushroom ("BloodPuke",10)
	BOSX O -1 A_BossDeath
	Stop
	XDeath: //new
	BOSX I 0 A_SpawnDebris("Gore_OnyxGib11",0)
	BOSX I 0 A_SpawnDebris("NashGore_Gib11",0)
	BOSX I 0 A_SpawnDebris("Gore_OnyxGib11",0)
	BOSX I 0 A_SpawnDebris("NashGore_Gib11",0)
	BOSX I 0 A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,0)
	BOSX I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	BOSX I 8 A_Scream
	BOSX J 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
	BOSX J 8 A_PlaySound("profane/die")
	BOSX K 0 A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,6)
	BOSX K 8
	BOSX L 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,8)
	BOSX L 8 A_NoBlocking
	BOSX MN 8 A_Mushroom ("BloodPuke",10)
	BOSX O -1 A_BossDeath
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMedium",0,0,0,0)
	stop
	//can't be raised
	}
}

actor PoisonBall : CStaffMissile
{
radius 3
height 3
damage 10
speed 20
Renderstyle Add
Alpha 0.90
Poisondamage 10
Damagetype PoisonCloud
seesound "profane/poison"
deathsound "profane/poisondie"
PROJECTILE
+EXPLODEONWATER
states
	{
	Spawn:
	FVN1 A 1 A_BishopMissileWeave
	FVN1 A 0 A_SpawnItemEx("PoisonTrail",0,0,0,0,0,0,0,0,10)
	FVN1 B 1 A_BishopMissileWeave
	FVN1 B 0 A_SpawnItemEx("PoisonTrail",0,0,0,0,0,0,0,0,10)
	FVN1 C 1 A_BishopMissileWeave
	FVN1 C 0 A_SpawnItemEx("PoisonTrail",0,0,0,0,0,0,0,0,10)
	FVN1 D 1 A_BishopMissileWeave
	FVN1 D 0 A_SpawnItemEx("PoisonTrail",0,0,0,0,0,0,0,0,10)
	loop
	Death:
	FVN1 EFGHIJ 4
	stop
	}
}

actor BloodPuke
{
radius 4
height 4
damage 7
speed 20
Scale 0.80
deathsound "profane/blood"
PROJECTILE
+EXTREMEDEATH
+NOTELESTOMP
+NOTELEFRAG
-NOGRAVITY
states
	{
	Spawn:
	FGOR ABCDEF 3 bright A_SpawnItemEx ("Blood",0,0,0,0,0,0,0,0,10)
	loop
	Death:
	FGOR GHIJ 5 bright
	stop
	}
}

ACTOR PoisonTrail //trail sprites connections with Vulgar
{
Radius 0
Height 1
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.80
States
	{
	Spawn:
	NULL A 1 Bright
	SSFX ABCDEFG 2 Bright
	Stop
	}
}