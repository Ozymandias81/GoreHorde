actor ImpWarlord 30140
{
//$Category Realm667 Monsters
Tag "Imp Warlord"
Game Doom
spawnid 122
gibhealth 35
Health 180
Radius 20
Height 56
Speed 9
PainChance 128
MONSTER
+FLOORCLIP
SeeSound "imp/sight"
PainSound "imp/pain"
DeathSound "imp/death"
ActiveSound "imp/active"
MeleeSound "implord/macehit"
Obituary "%o couldn't run from the imp warlord's fireball."
HitObituary "%o was beaten to death by an imp warlord."
MeleeDamage 7
DropItem "SmallPotionsSpawner", 128
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	WARI AB 10 A_Look
	Loop
	See:
	WARI AABBCCDD 3 A_Chase
	Loop
	Melee:
	WARI E 6 A_FaceTarget
	WARI E 0 A_PlaySound("implord/maceswing")
	WARI F 4 A_FaceTarget
	WARI G 5 A_MeleeAttack
	Goto See
	Missile:
	WARI E 0 A_Jump(38,23)  //20% chance: jump to familiar drop
	WARI E 0 A_Jump(77,2)   //30% chance: jump to heat seekers
	WARI E 0 A_Jump(128,11) //50% chance: jump to green fireballs
	Goto Missile+28         //Otherwise : jump to red fireball
	WARI I 0 A_SetInvulnerable
	WARI I 6 A_FaceTarget
	WARI J 0 A_PlaySound("implord/shieldshot")
	WARI J 6 bright A_FaceTarget
	WARI J 0 A_Jump(128,4)
	WARI K 0 bright A_CustomMissile("YellowSkullSeeker", 32, 0, -45)
	WARI K 6 bright A_CustomMissile("YellowSkullSeeker", 32, 0, 45)
	WARI K 0 A_UnSetInvulnerable
	Goto See
	WARI K 6 bright A_CustomMissile("YellowSkullSeeker", 32, 0, 0)
	WARI K 0 A_UnSetInvulnerable
	Goto See
	WARI L 7 A_FaceTarget
	WARI M 0 A_PlaySound("implord/maceswing")
	WARI M 5 A_FaceTarget
	WARI M 0 A_PlayWeaponSound("imp/attack")
	WARI M 0 A_Jump(128,3)
	WARI N 0 A_CustomMissile("GreenPoisonBall", 40, 0, -8, 1)
	WARI N 0 A_CustomMissile("GreenPoisonBall", 24, 0, 8, 1)
	WARI N 0 A_CustomMissile("GreenPoisonBall", 32, 0, 0, 1)
	WARI N 0 A_CustomMissile("GreenPoisonBall", 36, 0, -4, 1)
	WARI N 6 A_CustomMissile("GreenPoisonBall", 28, 0, 4, 1)
	Goto See
	WARI GF 8 A_FaceTarget
	WARI F 0 A_Jump(128,2)
	WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, -30)
	Goto See
	WARI E 8 A_CustomMissile("FamiliarSpawner", 32, 0, 30)
	Goto See
	WARI O 0 A_Jump(128,5)
	WARI O 7 A_FaceTarget
	WARI P 0 A_PlaySound("implord/maceswing")
	WARI P 5 A_FaceTarget
	WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
	WARI O 7 A_FaceTarget
	WARI P 0 A_PlaySound("implord/maceswing")
	WARI P 5 A_FaceTarget
	WARI Q 6 A_CustomMissile("RedSkullBall", 32, 0, 0)
	Goto See
	Pain:
	WARI H 0 A_Jump(128,2)
	WARI H 8 A_Pain
	Goto See
	WARI I 1
	WARI I 0 A_SetInvulnerable
	WARI IIIIII 4 A_FaceTarget
	WARI I 0 A_Jump(160,5)
	WARI IIII 4 A_FaceTarget
	Goto Pain+4
	WARI I 0 A_UnSetInvulnerable
	Goto See
	Death:
	WARI R 0 A_FaceTarget
	WARI R 0 A_SpawnItemEx("WarlordMace", 0, 0, 32, 4, 0, 0, -90, 128)
	WARI R 0 A_SpawnItemEx("WarlordShield", 0, 0, 32, 5, 0, 0, 90, 128)
	WARI R 8
	WARI S 8 A_Scream
	WARI T 6
	WARI U 6 A_Fall
	WARI V -1
	Stop
	XDeath: //new
	WARI R 0 A_SpawnDebris("NashGore_Gib11",0)
	WARI R 0 A_SpawnDebris("NashGore_Gib11",0)
	WARI R 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	WARI R 0 A_FaceTarget
	WARI R 0 A_SpawnItemEx("WarlordMace", 0, 0, 32, 4, 0, 0, -90, 128)
	WARI R 0 A_SpawnItemEx("WarlordShield", 0, 0, 32, 5, 0, 0, 90, 128)
	WARI R 8
	WARI S 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,1)
	WARI S 8 A_Scream
	WARI T 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	WARI T 6
	WARI U 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,3)
	WARI U 6 A_Fall
	WARI V -1
	Stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
	stop
	Raise:
	WARI VU 8
	WARI TSR 6
	Goto See
	}
}

actor YellowSkullSeeker
{
Radius 8
Height 6
Speed 9
Damage 3
+SEEKERMISSILE
PROJECTILE
RENDERSTYLE ADD
scale 0.67
ALPHA 0.67
DeathSound "implord/shieldshothit"
Decal RevenantScorch
States
	{
	Spawn:
	WLP2 ABCD 3 Bright A_SeekerMissile (5,10)
	Loop
	Death:
	WLP2 EFG 4 Bright
	Stop
	}
}

actor GreenPoisonBall
{
Radius 8
Height 6
Speed 10
Damage 3
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.67
DeathSound "imp/shotx"
States
	{
	Spawn:
	WLP1 ABCD 4
	Loop
	Death:
	WLP1 EFGH 4
	Stop
	}
}

ACTOR FamiliarSpawner
{
+MISSILE
+DOOMBOUNCE
Speed 8
Damage 0
DeathSound "implord/familiarspawn"
DropItem DragonFamiliar
States
	{
	Spawn:
	WLP3 ABCD 4
	Loop
	Death:
	WLP3 E 4
	WLP3 F 6
	WLP3 F 0 A_Jump(85,3)
	WLP3 G 8 A_CustomMissile("BatFamiliar", 32, 0, 0)
	WLP3 H 350
	Stop
	WLP3 G 8 A_CustomMissile("DragonFamiliar", 32, 0, 0)
	WLP3 H 350
	Stop
	}
}

actor RedSkullBall
{
Radius 8
Height 6
Speed 15
Damage 12
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.67
SeeSound "imp/attack"
DeathSound "imp/shotx"
Decal PlasmaScorchLower
States
	{
	Spawn:
	WLP4 ABCB 4
	Loop
	Death:
	WLP4 DEFG 4
	Stop
	}
}

actor WarlordMace
{
+DOOMBOUNCE
Speed 4
States
	{
	Spawn:
	WLI2 ABCDEF 5
	WLI2 G -1
	Stop
	}
}

actor WarlordShield
{
+DOOMBOUNCE
Speed 5
States
	{
	Spawn:
	WLI1 ABCDEF 5
	WLI1 G -1
	Stop
	}
}

actor BatFamiliar 30141
{
//$Category Realm667 Monsters
Tag "Imp Warlord's Bat Familiar"
spawnid 123
Health 10
Radius 12
Height 24
Speed 20 //superfast!
PainChance 200
MONSTER
+FLOAT
+NOGRAVITY
+FASTMELEE
SeeSound "batfam/idle"
PainSound "batfam/pain"
DeathSound "batfam/death"
ActiveSound "batfam/idle"
MeleeSound "batfam/idle"
HitObituary "%o was scratched by a bat familiar"
MISSILEHEIGHT 0
MeleeDamage 1
States
	{
	Spawn:
	BFAM ABCB 3 A_Look
	Loop
	See:
	BFAM ABCB 3 A_Chase
	Loop
	Melee:
	BFAM A 3 A_FaceTarget
	BFAM B 3 A_MeleeAttack
	BFAM CB 3 A_FaceTarget
	Goto See
	Pain:
	BFAM A 2
	BFAM A 2 A_Pain
	Goto See
	Death:
	BFAM D 0 A_Die
	BFAM D 0 A_NoBlocking
	BFAM D 5 A_Scream
	BFAM E 5
	BFAM FDEF 5
	Stop //Goto Death+5
	Crash:
	BFAM G 0 A_SetFloorClip
	BFAM G 6 A_PlaySound("batfam/crash")
	BFAM H 7
	BFAM I 8
	BFAM J -1
	Stop
	}
}

actor DragonFamiliar 30142
{
//$Category Realm667 Monsters
Tag "Imp Warlord's Dragon Familiar"
spawnid 124
Health 25
Radius 16
Height 32
Speed 10
PainChance 200
MONSTER
+FLOAT
+NOGRAVITY
+STRIFEDAMAGE
SeeSound "dragonfam/see"
PainSound "dragonfam/pain"
DeathSound "dragonfam/death"
ActiveSound "dragonfam/idle"
Obituary "%o was cooked by a dragon familiar."
States
	{
	Spawn:
	DFAM ABCD 10 A_Look
	Loop
	See:
	DFAM AABBCCDD 3 A_Chase
	Loop
	Missile:
	DFAM E 0 A_PlaySound("dragonfam/attack")
	DFAM E 0 A_Jump(208,7)
	DFAM E 2 Bright A_FaceTarget
	DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
	DFAM E 2 Bright A_FaceTarget
	DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
	DFAM E 2 Bright A_FaceTarget
	DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
	Goto See
	DFAM E 2 Bright A_FaceTarget
	DFAM E 0 A_CustomMissile("MiniFirePuffNoDamage", 12, 0, 0)
	DFAM E 2 Bright A_FaceTarget
	DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, 2)
	DFAM E 2 Bright A_FaceTarget
	DFAM E 0 A_CustomMissile("MiniFirePuff", 12, 0, -2)
	Goto Missile+1
	Pain:
	DFAM F 2
	DFAM F 2 A_Pain
	Goto See
	Death:
	DFAM G 0 A_Die
	DFAM G 8 A_NoBlocking
	DFAM H 8 A_Scream
	DFAM I 6 A_SetFloorClip
	DFAM JKL 6
	DFAM M -1
	Stop
	}
}

actor MiniFirePuff
{
Radius 6
Height 10
Speed 7
Damage 1
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.67
States
	{
	Spawn:
	DFFP ABCDEFGH 2 Bright
	Death:
	DFFP IJKLMNOPQR 2 Bright
	Stop
	}
}

actor MiniFirePuffNoDamage : MiniFirePuff {Damage 0}

Actor BatsFlockSpawner 8899 //new
{
//$Category Special Effects
Tag "Bats Flock Spawner"
Game Doom
+NOGRAVITY
+NOBLOCKMAP
+NOINTERACTION
+CLIENTSIDEONLY
States
	{
	Spawn:
	TNT1 A 1
	TNT1 A 0 A_Jump(255,"Five","Ten","Twenty","Fifty")
	Five:
	TNT1 A 1
	TNT1 AAAAA 0 A_SpawnItemEx("BatFamiliar", random(-192,192), random(-192,192), 0, 0, 0, 0, random(0,360), 0)
	Stop
	Ten:
	TNT1 A 1
	TNT1 AAAAAAAAAA 0 A_SpawnItemEx("BatFamiliar", random(-192,192), random(-192,192), 0, 0, 0, 0, random(0,360), 0)
	Stop
	Twenty:
	TNT1 A 1
	TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BatFamiliar", random(-192,192), random(-192,192), 0, 0, 0, 0, random(0,360), 0)
	Stop
	Fifty:
	TNT1 A 1
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BatFamiliar", random(-192,192), random(-192,192), 0, 0, 0, 0, random(0,360), 0)
	Stop
	}
}