actor Vulgar : DoomImp 25630 //trail sprites connections with Grell & ProfaneOne
{
//$Category Realm667 Monsters
Tag "Vulgar"
Translation "16:47=112:127","168:191=114:128"
spawnid 160
obituary "%o was poisoned by a vulgar."
hitobituary "A Vulgar had %o for lunch."
health 70
gibhealth 35
radius 20
height 56
mass 120
speed 14 //veryfast!
painchance 180
MeleeDamage 4
MONSTER
+FLOORCLIP
+DONTHURTSPECIES
DamageFactor Poison, 0
DamageFactor PoisonCloud, 0
seesound "monster/vulsit"
painsound "monster/vulpai"
deathsound "monster/vuldth"
activesound "monster/vulact"
meleesound "monster/vulhit"
DropItem "Clip", 128
DropItem "Clip", 32
DropItem "ArmorShard", 16
Bloodcolor "0 a3 0"
MaxDropOffHeight 32
states
	{
	Spawn:
	VULG AB 10 A_Look
	loop
	See:
	VULG AABBCCDD 3 A_Chase
	loop
	Melee:
	Missile:
	VULG E 8 A_FaceTarget
	VULG F 1 A_PlaySound("monster/vulswg")
	VULG F 7 A_FaceTarget
	VULG G 6 A_MeleeAttack
	goto See
	Missile:
	VULG EF 8 A_FaceTarget
	VULG G 6 A_CustomMissile("VulgarShot",37,0,0,0,0)
	goto See
	Pain:
	VULG H 2
	VULG H 2 A_Pain
	goto See
	Death:
	VULG I 8
	VULG J 8 A_Scream
	VULG KL 6
	VULG M 6 A_NoBlocking
	VULG N -1
	stop
	XDeath: //new
	VULG O 0 A_SpawnDebris("Gore_GreenGib11",0)
	VULG O 0 A_SpawnDebris("Gore_GreenGib11",0)
	VULG O 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,0)
	VULG O 5
	VULG P 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,2)
	VULG P 5 A_XScream
	VULG Q 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,4)
	VULG Q 5
	VULG R 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,6)
	VULG R 5 A_NoBlocking
	VULG STU 5
	VULG V -1
	stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedSmallGreen",0,0,0,0)
	stop
	Raise:
	VULG ML 8
	VULG KJI 6
	goto See
	}
}

ACTOR VulgarShot
{
Radius 8
Height 16
Speed 10
Damage 4
PoisonDamage 32
RENDERSTYLE ADD
ALPHA 0.67
PROJECTILE
+THRUGHOST
Seesound "poly/stmdr1"
DeathSound "weapons/bloodx"
States
	{
	Spawn:
	FVUL AAABBB 1 Bright A_SpawnItemEx("VBarbTrail",0,0,0,0,0,0,0,128,0)
	loop
	Death:
	FVUL CDEF 4 Bright
	stop
	}
}

ACTOR VBarbTrail //renamed to prevent issues
{
Radius 0
Height 1
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.75
States
	{
	Spawn:
	NULL A 1 Bright
	SSFX ABCDEFG 2 Bright
	Stop
	}
}