ACTOR Cybruiser : BaronOfHell 30128 //Main Actor sprites renamed due to prevent doubles with Bruiser Demon
{
//$Category Realm667 Monsters
Tag "Cybruiser"
spawnid 107
gibhealth 35
Health 2000
Radius 24
Height 64
Mass 1000
Speed 8
PainChance 20
MONSTER
MeleeDamage 10
Obituary "%o was blown away by a Cybruiser."
HitObituary "%o was smashed by a Cybruiser."
+FLOORCLIP
+MISSILEMORE
+BOSS
+DONTHURTSPECIES
+DONTGIB
SeeSound "monster/brusit"
PainSound "baron/pain"
DeathSound "monster/brudth"
MeleeSound "baron/melee"
DropItem "RocketBox", 48
DropItem "RocketAmmo", 24, 2
DropItem "MediumPotionsSpawner", 8
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	YBRS AB 10 A_Look
	Loop
	See:
	YBRS A 1 A_Playsound("monster/bruwlk")
	YBRS A 2 A_Chase
	YBRS A 3 A_Chase
	YBRS BB 3 A_Chase
	YBRS C 1 A_Playsound("monster/bruwlk")
	YBRS C 2 A_Chase
	YBRS C 3 A_Chase
	YBRS DD 3 A_Chase
	Loop
	Missile:
	YBRS E 8 A_FaceTarget
	YBRS F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
	YBRS E 12 A_FaceTarget
	YBRS F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
	YBRS E 12 A_FaceTarget
	YBRS F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
	Goto See
	Melee:
	YBRS GH 8 A_FaceTarget
	YBRS I 8 A_MeleeAttack
	Goto See
	Pain:
	YBRS J 2
	YBRS J 2 A_Pain
	Goto See
	Death:
	YBRS K 6 Bright A_Scream
	YBRS LMN 6 Bright
	YBRS O 6 Bright A_NoBlocking
	YBRS QRS 6 Bright
	YBRS T -1 A_BossDeath
	Stop
	XDeath:
	YBRS K 0 A_SpawnDebris("NashGore_Gib11",0)
	YBRS K 0 A_SpawnDebris("NashGore_Gib11",0)
	YBRS K 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	YBRS K 6 Bright A_Scream
	YBRS L 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	YBRS M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
	YBRS N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
	YBRS LMN 6 Bright
	YBRS O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,8)
	YBRS O 6 Bright A_NoBlocking
	YBRS QRS 6 Bright
	YBRS T -1 A_BossDeath
	Stop
	Raise:
	YBRS TSRQPONMLK 2
	goto See
	}
}

ACTOR BruiserMissile
{
Radius 8
Height 12
Speed 20
Damage 10
Scale 1.0
PROJECTILE
RENDERSTYLE Normal
+THRUGHOST
SeeSound "monster/brufir"
DeathSound "weapons/hellex"
DONTHURTSHOOTER
+FIREDAMAGE
States
	{
	Spawn:
	FBRS A 1 Bright
	FBRS A 1 Bright A_SpawnItem("BruiserTrail",0,0)
	loop
	Death:
	BAL3 C 0 Bright A_SetTranslucent (0.67,1)
	BAL3 C 6 Bright
	BAL3 D 6 Bright A_Explode(64,64,0)
	BAL3 E 6 Bright
	stop
	}
}

ACTOR BruiserTrail
{
Radius 3
Height 3
RENDERSTYLE Translucent
Alpha 0.67
PROJECTILE
	States
	{
	Spawn:
	NULL A 3 Bright
	PUFF ABCD 4 BRIGHT
	Stop
	}
}