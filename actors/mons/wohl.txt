ACTOR WarlordOfHell 22282
{
//$Category Realm667 Monsters
Tag "Warlord Of Hell"
Game Doom
spawnid 150
Health 1500
gibhealth 35
Radius 20
Height 56
Speed 13
PainChance 50
Scale 1.2
Mass 1000
DamageFactor Ice, 0.5
DamageFactor Poison, 0.5
DamageFactor PoisonCloud, 0.5
MONSTER
+FLOORCLIP
Obituary "%o was extuingished by the warlord of hell."
SeeSound "warlord/sight"
PainSound "warlord/pain"
DeathSound "warlord/death"
ActiveSound "warlord/active"
DropItem "BackPack", 8
DropItem "BlurSphere", 16
DropItem "ShellBox", 96, 2
BloodColor "18 2 F0"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	BOS6 AB 10 A_Look
	Loop
	See:
	BOS6 AABBCCDD 4 A_Chase
	Loop
	Missile:
	BOS6 E 0 A_JumpIfHealthLower(500,13)
	BOS6 E 0 A_JumpIfHealthLower(1000,4)
	BOS6 E 6 A_FaceTarget
	BOS6 F 6 A_FaceTarget
	BOS6 G 6 A_Custommissile ("WarlordOfHellBall", 32, 0, 0)
	Goto See
	BOS6 E 6 A_FaceTarget
	BOS6 F 6 A_FaceTarget
	BOS6 G 0 A_Custommissile ("WarlordOfHellBall", 32, 0, 8)
	BOS6 G 6 A_Custommissile ("WarlordOfHellBall", 32, 0, 0)
	BOS6 P 6 A_FaceTarget
	BOS6 Q 6 A_FaceTarget
	BOS6 R 0 A_Custommissile ("WarlordOfHellBall", 32, 0, 0)
	BOS6 R 6 A_Custommissile ("WarlordOfHellBall", 32, 0, -8)
	Goto See
	BOS6 E 6 A_FaceTarget
	BOS6 F 6 A_FaceTarget
	BOS6 G 0 A_Custommissile ("WarlordOfHellBall", 32, 0, -20)
	BOS6 G 6 A_Custommissile ("WarlordOfHellBall", 32, 0, 0)
	BOS6 P 6 A_FaceTarget
	BOS6 Q 6 A_FaceTarget
	BOS6 R 0 A_Custommissile ("WarlordOfHellBall", 32, 0, 20)
	BOS6 R 6 A_Custommissile ("WarlordOfHellBall", 32, 0, 0)
	BOS6 E 6 A_FaceTarget
	BOS6 F 6 A_FaceTarget
	BOS6 G 0 A_Custommissile ("WarlordOfHellBall", 32, 0, -12)
	BOS6 G 6 A_Custommissile ("WarlordOfHellBall", 32, 0, 12)
	Goto See
	Pain:
	BOS6 H 5
	BOS6 H 5 A_Pain
	Goto See
	Death:
	BOS6 I 12
	BOS6 J 12 A_Scream
	BOS6 K 12 A_Fall
	BOS6 LMN 12
	BOS6 O -1
	Stop
	XDeath:
	BOS6 I 0 A_SpawnDebris("Gore_BlueGib11",0)
	BOS6 I 0 A_SpawnDebris("Gore_OnyxGib11",0)
	BOS6 I 0 A_SpawnDebris("Gore_BlueGib11",0)
	BOS6 I 0 A_SpawnItem("Gore_GibGeneratorBlue",0,0,0,0)
	BOS6 I 12
	BOS6 J 0 A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,2)
	BOS6 J 12 A_Scream
	BOS6 K 0 A_SpawnItem("Gore_GibGeneratorBlue",0,0,0,4)
	BOS6 K 12 A_Fall
	BOS6 L 0 A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,6)
	BOS6 LMN 12
	BOS6 O -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumBlue",0,0,0,0)
	stop
	Raise:
	BOS6 ONMLKJI 5
	Goto See
	}
}

actor WarlordOfHellBall
{
Radius 6
Height 8
Speed 15
Damage 8
PROJECTILE
RENDERSTYLE NORMAL
ALPHA 1.0
SeeSound "imp/attack"
DeathSound "imp/shotx"
Decal BaronScorch
States
	{
	Spawn:
	BALM AB 4 Bright
	Loop
	Death:
	BALM CDE 6 Bright
	Stop
	}
}