Actor QuadShotgunZombie 30119
{
//$Category Realm667 Monsters
Tag "Quad-Shotgun Zombie"
Game Doom
spawnid 136
gibhealth 35
Health 250
Radius 20
Height 56
Mass 100
Speed 8
PainChance 60
MONSTER
+FLOORCLIP
SeeSound "ShotGuy/Sight"
PainSound "ShotGuy/Pain"
DeathSound "ShotGuy/Death"
ActiveSound "ShotGuy/Active"
Obituary "%o was smeared by a Quad-Shotgun Zombie"
DropItem "Shell", 64, 2
DropItem "ShellBox", 8
DropItem "QuadShotgun", 32
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	QSZM A 0
	QSZM A 0 A_GiveInventory("BreathToken", 1)
	QSZM AAAAAAAAAABBBBBBBBBB 1 A_Look
	Loop
	See:
	QSZM AABBCCDD 3 A_Chase
	Loop
	Missile:
	QSZM E 0 A_JumpIfInventory("QuadShotgunZombieCounter", 1, "SingleShot")
	QSZM E 0 A_Jump(64, "QuadShot")
	Goto SingleShot
	SingleShot: //new
	QSZM EE 5 A_FaceTarget
	QSZM F 0 Bright A_PlayWeaponSound("QuadShotgunZombie/SingleFire")
	QSZM F 0 Bright A_GiveInventory("QuadShotgunZombieCounter", 1)
	QSZM F 0 Bright A_SpawnItemEx("ShotCaseSpawn", random(20,24), -4, random(24,28), 0, 0, 0, 0, random(128,132))
	QSZM F 4 Bright A_CustomBulletAttack(6, 6, 5, 3, "Bulletpuff")
	TNT1 A 0 A_JumpIf(waterlevel > 0,2)
	TNT1 A 0 A_SpawnItemEx("ShotSmokeSpawner",10,8,30,momx,momy,random(1,3),0,0,0)
	QSZM E 6 A_FaceTarget
	QSZM E 0 A_JumpIfInventory("QuadShotgunZombieCounter", 4, "Reload")
	QSZM E 0 A_Jump(96, "SingleShot")
	QSZM EE 6 A_FaceTarget
	Goto See
	QuadShot: //new
	QSZM E 0 A_JumpIfCloser(384, 1)
	Goto SingleShot
	QSZM EEE 5 A_FaceTarget
	QSZM F 0 Bright A_PlayWeaponSound("QuadShotgunZombie/QuadFire")
	QSZM F 0 Bright A_GiveInventory("QuadShotgunZombieCounter", 4)
	QSZM F 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), -2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
	QSZM F 0 A_SpawnItemEx("ShotCaseSpawn", random(20,24), -4, random(24,28), 0, 0, 0, 0, random(128,132))
	QSZM F 0 A_SpawnItemEx("SShotCaseSpawn", random(20,24), -2, random(30,34), 0, 0, 0, random(180,184), random(128,132))
	QSZM F 0 A_SpawnItemEx("ShotCaseSpawn", random(20,24), -4, random(24,28), 0, 0, 0, 0, random(128,132))
	QSZM F 4 Bright A_CustomBulletAttack(11, 11, 20, 3, "Bulletpuff")
	TNT1 A 0 A_JumpIf(waterlevel > 0,5)
	TNT1 AAAA 0 A_SpawnItemEx("ShotSmokeSpawner",random(8,10),8,30,momx,momy,random(1,3),0,0,0)
	QSZM EE 6 A_FaceTarget
	Goto Reload
	Reload:
	QSZM E 0 A_ChangeFlag("NoPain", 1)
	QSZM E 10 A_PlaySoundEx("QuadShotgunZombie/Reload1", "SoundSlot7", 0)
	QSZM E 0 A_TakeInventory("QuadShotgunZombieCounter", 1)
	QSZM E 10 A_PlaySoundEx("QuadShotgunZombie/Reload2", "SoundSlot6", 0)
	QSZM E 0 A_JumpIfInventory("QuadShotgunZombieCounter", 1, 1)
	Goto Reload+6
	QSZM E 0
	Goto Reload+2
	QSZM E 10 A_PlaySoundEx("QuadShotgunZombie/Reload3", "SoundSlot7", 0)
	QSZM E 0 A_ChangeFlag("NoPain", 0)
	Goto See
	Pain:
	QSZM G 3
	QSZM G 3 A_Pain
	Goto See
	Death:
	QSZM H 8
	QSZM I 8 A_Scream
	QSZM J 8 A_NoBlocking
	QSZM K 8
	QSZM L -1
	Stop
	XDeath: //new
	QSZM M 0 A_SpawnDebris("NashGore_Gib9",0)
	QSZM M 0 A_SpawnDebris("NashGore_Gib10",0)
	QSZM M 0 A_SpawnDebris("NashGore_Gib11",0)
	QSZM M 0 A_SpawnDebris("NashGore_Gib11",0)
	QSZM M 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	QSZM M 5
	QSZM N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	QSZM N 5 A_XScream
	QSZM O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
	QSZM O 5 A_NoBlocking
	QSZM P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
	QSZM PQRST 5
	QSZM U -1
	Stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
	stop
	Raise:
	QSZM L 5
	QSZM KJIH 5
	Goto See
	}
}

Actor QuadShotgunZombieCounter : Inventory {Inventory.MaxAmount 4}