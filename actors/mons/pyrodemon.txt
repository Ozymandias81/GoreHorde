ACTOR PyroDemon 30105 //Some actors/sprites renamed due to prevent connections with Afrit
{
//$Category Realm667 Monsters
Tag "Pyro Demon"
Game Doom
spawnid 134
Health 2500
Radius 24
Height 64
Mass 1000
Speed 8
PainChance 20
MONSTER
MeleeDamage 10
DamageFactor Fire, 0.5
DamageFactor HellFire, 0.5
OBITUARY "%o was burnt to a crisp by a pyro demon"
HITOBITUARY "%o was melted my a pyro demon"
+NOTARGET
+FLOORCLIP
+MISSILEMORE
+NORADIUSDMG
+BOSS
+DONTHURTSPECIES
+NOFEAR //added by me
+DONTGIB //added by me
SEESOUND "pyro/see"
PAINSOUND "pyro/pain"
DEATHSOUND "pyro/death"
ACTIVESOUND "pyro/idle"
DropItem "BigGas", 128, 2
DropItem "RageSphere", 32
DropItem "GuardSphereST", 64
DropItem "RegenSphere", 16
DropItem "SoulSphere", 8
Bloodcolor "darkorange1"
MaxStepHeight 32
MaxDropOffHeight 32
states
	{
	Spawn:
	PYRO AB 10 A_Look
	Loop
	See:
	PYRO AA 3 A_Chase
	PYRO A 0 A_SpawnItemEx("WalkFire",0,0,0,1,0,0,0,128)
	PYRO BB 3 A_Chase
	PYRO B 0 A_SpawnItemEx("WalkFire",0,0,0,1,0,0,0,128)
	PYRO CC 3 A_Chase
	PYRO C 0 A_SpawnItemEx("WalkFire",0,0,0,1,0,0,0,128)
	PYRO DD 3 A_Chase
	PYRO D 0 A_SpawnItemEx("WalkFire",0,0,0,1,0,0,0,128)
	Loop
	Missile:
	PYRO B 0 A_Jump(32,29)
	PYRO B 0 A_Jump(32,20)
	PYRO EF 8 BRIGHT A_FaceTarget
	PYRO G 0 BRIGHT A_CustomMissile ("FireShot",28,0,0,0)
	PYRO G 0 BRIGHT A_CustomMissile ("FireShot",28,0,-4,0)
	PYRO G 3 BRIGHT A_CustomMissile ("FireShot",28,0,4,0)
	PYRO H 3 BRIGHT
	PYRO H 0 A_Jump(32,8)
	PYRO IJ 8 BRIGHT A_FaceTarget
	PYRO K 0 BRIGHT A_CustomMissile ("FireShot",28,0,0,0)
	PYRO K 0 BRIGHT A_CustomMissile ("FireShot",28,0,-4,0)
	PYRO K 3 BRIGHT A_CustomMissile ("FireShot",28,0,4,0)
	PYRO L 3 BRIGHT
	PYRO L 0 A_Jump(32,1)
	Goto See
	PYRO MN 8 BRIGHT A_FaceTarget
	PYRO O 4 BRIGHT A_CustomMissile ("PyroBlast",28,0,0,0)
	PYRO P 4 BRIGHT
	Goto See
	PYRO MN 8 BRIGHT A_FaceTarget
	PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,0,0)
	PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,90,0)
	PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,180,0)
	PYRO O 4 BRIGHT A_CustomMissile ("RainCometNull",28,0,270,0)
	PYRO P 4 BRIGHT
	Goto See
	PYRO O 1 A_FaceTarget
	PYRO O 0 A_CustomMissile ("EscapeExplosion",28,10,0,0)
	PYRO O 0 A_Mushroom ("Pyroball",16)
	PYRO O 3
	PYRO P 3 BRIGHT
	PYRO A 0 A_UnSetShootable
	PYRO A 1 A_PlaySound("pyro/tele")
	PYRO A 1 A_SetTranslucent(0.90)
	PYRO A 1 A_SetTranslucent(0.80)
	PYRO A 1 A_SetTranslucent(0.70)
	PYRO A 1 A_SetTranslucent(0.60)
	PYRO A 1 A_SetTranslucent(0.50)
	PYRO A 1 A_SetTranslucent(0.40)
	PYRO A 1 A_SetTranslucent(0.30)
	PYRO A 1 A_SetTranslucent(0.20)
	PYRO A 1 A_SetTranslucent(0.10)
	NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
	NULL A 0 A_Jump(128,25)
	NULL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
	PYRO A 1 A_PlaySound("pyro/tele")
	PYRO A 1 A_SetTranslucent(0.10)
	PYRO A 1 A_SetTranslucent(0.20)
	PYRO A 1 A_SetTranslucent(0.30)
	PYRO A 1 A_SetTranslucent(0.40)
	PYRO A 1 A_SetTranslucent(0.50)
	PYRO A 1 A_SetTranslucent(0.60)
	PYRO A 1 A_SetTranslucent(0.70)
	PYRO A 1 A_SetTranslucent(0.80)
	PYRO A 1 A_SetTranslucent(0.90)
	PYRO A 1 A_SetTranslucent(1.0)
	PYRO A 0 A_CustomMissile ("EscapeExplosion",28,10,0,0)
	PYRO O 0 A_Mushroom ("Pyroball",16)
	PYRO A 0 A_SetShootable
	Goto See
	Pain:
	PYRO Q 2
	PYRO Q 2 A_Pain
	Goto See
	Death:
	PYRO R 20 BRIGHT A_Scream
	PYRO S 20 BRIGHT
	PYRO T 20 BRIGHT
	PYRO U 20 BRIGHT A_NoBlocking
	PYRO VW 6 BRIGHT
	PYRO X 6 BRIGHT
	PYRO Y 3 BRIGHT
	PYRO Z -1 BRIGHT
	Stop
	}
}

ACTOR FireShot
{
Radius 3
Height 3
Speed 15
Damage 3
PROJECTILE
RENDERSTYLE ADD
ALPHA 0.67
+FIREDAMAGE
SeeSound "pyro/flame"
DeathSound "pyro/flamepoof"
Decal RevenantScorch
States
	{
	Spawn:
	FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	FSHT A 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	Loop
	Death:
	FSHT A 0 Bright A_Explode
	FSHT B 0 Bright
	FSHT B 3 Bright
	FSHT C 3 Bright
	FSHT D 3 Bright
	FSHT E 3 Bright
	Stop
	}
}

actor FireTail
{
PROJECTILE
RENDERSTYLE ADD
+NOCLIP
ALPHA 0.4
States
	{
	Spawn:
	FRBT ABCDEFGHI 1 Bright
	Stop
	}
}

ACTOR EscapeExplosion
{
SeeSound "pyro/explosion"
Radius 8
Height 56
Speed 0
ExplosionDamage 50
ExplosionRadius 192
States
	{
	Spawn:
	NKXP B 3 BRIGHT
	NKXP C 3 BRIGHT
	NKXP D 3 BRIGHT A_Explode
	NKXP EFGHIJKLMNOPQRSTUV 3 BRIGHT
	Stop
	}
}

ACTOR PyroBall
{
Radius 10
Height 8
Speed 15
SEESOUND "pyro/bomb"
DEATHSOUND "pyro/bombhit"
PROJECTILE
RENDERSTYLE Add
Alpha 0.7
-NoGravity
+DoomBounce
+LowGravity
+FireDamage
+SeekerMissile
Decal RevenantScorch
states
	{
	Spawn:
	RBAL A 0 BRIGHT
	RBAL A 0 BRIGHT A_AlertMonsters
	RBAL AAAABBBB 1 BRIGHT
	Goto Spawn +2
	Death:
	RBAL DEFGHI 4 BRIGHT
	Stop
	}
}

ACTOR FirebombExplosion1
{
Radius 11
Height 8
Speed 18
Damage 6
+NOBLOCKMAP
+DROPOFF
+MISSILE
+ACTIVATEIMPACT
+ACTIVATEPCROSS
ExplosionDamage 128
ExplosionRadius 144
Obituary "%o was killed by a Firebomb explosion."
States
	{
	Spawn:
	FRME A 1 Bright
	Loop
	Death:
	FRME A 0
	FRME A 0 A_PlaySound("pyro/blastexplosion")
	FRME A 3 Bright A_Explode
	FRME B 1 Bright A_Mushroom("Pyroball",8)
	FRME B 2 Bright A_CustomMissile("FirebombExplosion2",25,0,180)
	FRME CDEFGHIJKLMNOPQRS 3 Bright
	Stop
	}
}

ACTOR FirebombExplosion2 : FirebombExplosion1
{
ExplosionRadius 136
States
	{
	Death:
	FRME A 0
	FRME A 0 A_PlaySound("pyro/blastexplosion")
	FRME A 3 Bright A_Explode
	FRME B 1 Bright
	FRME B 2 Bright A_CustomMissile("FirebombExplosion3",20,0,180)
	FRME CDEFGHIJKLMNOPQRS 3 Bright
	Stop
	}
}

ACTOR FirebombExplosion3 : FirebombExplosion1
{
Speed 14
Damage 5
ExplosionRadius 128
States
	{
	Death:
	FRME A 0
	FRME A 0 A_PlaySound("pyro/blastexplosion")
	FRME A 3 Bright A_Explode
	FRME BCDEFGHIJKLMNOPQRS 3 Bright
	Stop
	}
}

ACTOR PyroBlast
{
PROJECTILE
+SEEKERMISSILE
+NOGRAVITY
RENDERSTYLE Add
Alpha 0.4
SEESOUND "pyro/blast"
DEATHSOUND "pyro/flamepoof"
Speed 17
Height 10
Radius 10
Decal Scorch
states
	{
	Spawn:
	SBLL ABCDEFGHIJ 1 BRIGHT A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
	SBLL A 0 BRIGHT A_SeekerMissile(0,2)
	Goto Spawn+1
	Death:
	SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,45)
	SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,135)
	SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,225)
	SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,315)
	SBLL LMNOPQRSTUVW 1 BRIGHT
	Stop
	}
}

ACTOR WalkFire
{
+NOCLIP
+MISSILE
+DROPOFF
+RANDOMIZE
Scale 1.5
Speed 1
RENDERSTYLE ADD
ALPHA 0.67
States
	{
	Spawn:
	NULL A 8
	FRFT A 3 Bright
	FRFT A 0
	FRFT B 3 Bright
	FRFT B 0
	FRFT C 3 Bright
	FRFT C 0
	FRFT C 0
	FRFT C 0 A_Jump(191,4)
	FRFT C 0 A_CustomMissile("WalkFire", 0, 24, 0)
	FRFT C 0 A_CustomMissile("WalkFire", 0, 0, 0)
	FRFT C 0 A_CustomMissile("WalkFire", 0, -24, 0)
	FRFT DEFGHIJKLMNO 3 Bright
	Stop
	}
}

ACTOR RainCometNull
{
Radius 0
Height 0
Speed 8
PROJECTILE
+FLOORCLIP
+RIPPER
+DOOMBOUNCE
+CEILINGHUGGER
+DONTHURTSPECIES
states
	{
	Spawn:
	ZAP5 A 0 A_Jump(5,8)
	ZAP5 AAAAAA 1 A_Wander
	ZAP5 A 8 A_CustomMissile("FallingComet",-24,random(0,128),random(0,360),2,random(1,89))
	Loop
	Death:
	ZAP5 A 0
	Stop
	}
}

ACTOR FallingComet
{
Radius 6
Height 8
Speed 5
Damage 5
ExplosionDamage 40
ExplosionRadius 80
Scale .55
PROJECTILE
SEESOUND "pyro/comet"
DEATHSOUND "pyro/cometdeath"
-NOGRAVITY
-FLOAT
+FLOORCLIP
+DONTHURTSPECIES
+SPAWNCEILING
Decal RevenantScorch
states
	{
	Spawn:
	COTM AAAABBBBCCCC 1 BRIGHT A_SpawnItemEx("FireTail", 0, 0, 0, 0, 0, 0, 0, 128)
	Loop
	Death:
	COTM D 3 BRIGHT A_SpawnItemEx("PCometDeathGlow", 0, 0, 0, 0, 0, 0, 0, 128)
	COTM E 3 BRIGHT A_Explode(20,128,0)
	COTM E 0 A_SpawnItemEx("PCometDeath", 0, 0, 0, 0, 0, 0, 0, 128)
	Stop
	}
}

actor PCometDeathGlow : FireTail
{
Scale 2.0
Alpha 0.6
States
	{
	Spawn:
	FRBT ABCDEFGHI 3 Bright
	Stop
	}
}

actor PCometDeath
{
PROJECTILE
RENDERSTYLE ADD
+NOCLIP
ALPHA 0.7
States
	{
	Spawn:
	COTM FGHI 3 Bright
	Stop
	}
}