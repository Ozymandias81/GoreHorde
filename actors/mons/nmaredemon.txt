ACTOR NightmareDemon 30110
{
//$Category Realm667 Monsters
Tag "Nightmare Demon"
Game Doom
translation "16:47=112:127","168:191=114:128"
spawnid 130
gibhealth 35
Health 260
Radius 30
Height 56
Speed 18
Mass 200
PainChance 200
RenderStyle TRANSLUCENT
Alpha 0.3
MONSTER
+FLOORCLIP
+LONGMELEERANGE
SeeSound "nightmareDemon/sight"
AttackSound "nightmareDemon/melee"
PainSound "nightmareDemon/pain"
DeathSound "nightmareDemon/death"
ActiveSound "nightmareDemon/active"
Obituary "%o was spooked by a Nightmare Demon."
DropItem "Cell", 64
BloodColor "0 a3 0"
BloodType "Gore_TGreenBlood"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	SAR2 AB 10 A_look
	Loop
	See:
	SAR2 AABBCCDD 2 A_Chase
	Loop
	Melee:
	SAR2 E 6 A_FaceTarget
	SAR2 F 6 A_FaceTarget
	SAR2 G 6 A_SargAttack
	Goto See
	Missile:
	SAR2 O 6 A_FaceTarget
	SAR2 P 8 A_CustomMissile("NDemonBall", 24, 0, 0)
	Goto See
	Pain:
	SAR2 H 3
	SAR2 H 3 A_Pain
	Goto See
	Death:
	SAR2 I 5
	SAR2 J 5 A_Scream
	SAR2 K 5
	SAR2 L 5 A_Fall
	SAR2 M 5
	SAR2 N -1
	Stop
	XDeath: //new
	SAR2 I 0 A_SpawnDebris("Gore_TGreenGib11",0)
	SAR2 I 0 A_SpawnItemEx("Gore_NDemonArm",10,0,32,0,random(2,8),0,0,random(0,180))
	SAR2 I 0 A_SpawnDebris("Gore_TGreenGib11",0)
	SAR2 I 0 A_SpawnItem("Gore_GibGeneratorTGreen",0,0,0,0)
	SAR2 I 5
	SAR2 J 0 A_SpawnItem("Gore_GibGeneratorTGreen",0,0,0,2)
	SAR2 J 5 A_Scream
	SAR2 K 0 A_SpawnItem("Gore_GibGeneratorTGreen",0,0,0,4)
	SAR2 K 5
	SAR2 L 0 A_SpawnItem("Gore_GibGeneratorTGreen",0,0,0,6)
	SAR2 L 5 A_Fall
	SAR2 M 5
	SAR2 N -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumTGreen",0,0,0,0)
	stop
	Raise:
	SAR2 MLKJI 5
	Goto See
	}
}

ACTOR NDemonBall
{
Radius 8
Height 6
Speed 12
Damage 4
PROJECTILE
RenderStyle TRANSLUCENT
Alpha 0.3
SeeSound "64ball/shoot"
DeathSound "64ball/explode"
Decal BaronScorch
States
	{
	Spawn:
	DBAL AB 4 Bright
	Loop
	Death:
	DBAL CDEFGH 4 Bright
	Stop
	}
}