ACTOR CyberImp 20002
{
//$Category Realm667 Monsters
Tag "Cyber Imp"
Game Doom
SpawnId 251
GibHealth 35
Health 150
Radius 20
Height 56
Mass 200
Speed 10
PainChance 150
Monster
+FLOORCLIP
+NOINFIGHTING
+DONTHURTSPECIES
SeeSound "CIMPSIT"
PainSound "CIMPPAIN"
DeathSound "CIMPDTH"
ActiveSound "CIMPACT"
HitObituary "%o was torn apart by a cyber imp"
Obituary "%o was fried by a cyber imp"
DropItem "Cell", 32
DropItem "SmallPotionsSpawner", 16
MaxDropOffHeight 32
States
	{
	Spawn:
	CIMP AB 10 A_Look
	Loop
	See:
	CIMP AABBCCDD 3 A_Chase
	Loop
	Melee:
	CIMP EF 8 A_FaceTarget
	CIMP F 6 A_CustommeleeAttack(3*random(3,10), "imp/melee")
	Goto See
	Missile: //slightly new
	CIMP EF 8 A_FaceTarget
	CIMP E 0 A_Jump(random(64,128),1,2)
	CIMP GG 6 A_CustomMissile("PlasmaBall",36,4)
	CIMP GG 6 A_CustomMissile("ArachnotronPlasma",36,4)
	Goto See
	Pain:
	CIMP H 2
	CIMP H 2 A_Pain
	Goto See
	Death:
	CIMP I 8
	CIMP J 8 A_Scream
	CIMP K 6
	CIMP L 6 A_NoBlocking
	CIMP M -1
	Stop
	XDeath:	//new
	CIMP N 0 A_SpawnDebris("NashGore_Gib9",0)
	CIMP N 0 A_SpawnDebris("NashGore_Gib10",0)
	CIMP N 0 A_SpawnDebris("NashGore_Gib11",0)
	CIMP N 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	CIMP N 5 A_CustomMissile("ImpCyberArm",32,-10,270)
	CIMP O 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	CIMP O 5 A_XScream
	CIMP P 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	CIMP P 5
	CIMP Q 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	CIMP Q 5 A_NoBlocking
	CIMP RST 5
	CIMP U -1
	Stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
	stop
	Raise:
	CIMP MLKJI 8
	Goto See
	}
}

ACTOR ImpCyberArm //renamed
{
Radius 8
Height 8
Speed 4
+DOOMBOUNCE
+DROPOFF
+Missile
States
	{
	Spawn:
	CARM ABCDE 3
	Loop
	Death:
	CARM F -1
	Loop
	}
}