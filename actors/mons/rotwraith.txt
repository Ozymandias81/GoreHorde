ACTOR RotWraith 30143
{
//$Category Realm667 Monsters
Tag "Rot Wraith"
Game Doom
spawnid 137
gibhealth 35
Health 150
PainChance 25
Speed 9
Height 55
Mass 75
MONSTER
+NOGRAVITY
+DROPOFF
+FLOAT
+FLOORCLIP
+TELESTOMP
+FLOATBOB
+DONTHARMCLASS
DamageFactor PoisonCloud, 0
DamageFactor Poison, 0
SeeSound "RotWraith/Sight"
PainSound "RotWraith/Pain"
DeathSound "RotWraith/Death"
ActiveSound "RotWraith/Active"
Obituary "%o was plagued by a RotWraith."
DropItem "SmallPotionsSpawner", 32
DropItem "Shell", 64
BloodColor "0 a3 0"
	States
	{
	Spawn:
	FARG AB 15 A_Look
	Loop
	See:
	FARG AABBCCDD 2 A_Chase
	Loop
	Pain:
	FARG A 2
	FARG H 6 A_Pain
	Goto See
	Melee:
	Missile:
	FARG E 6 A_FaceTarget
	FARG F 6
	FARG G 6 A_CustomMissile("FartBall", 32, 0)
	Goto See
	Death:
	FARG I 4 A_ChangeFlag("FLOATBOB", 0)
	FARG J 4 A_Scream
	FARG KL 4
	FARG M 4 A_NoBlocking
	FARG N 4
	FARG O 4
	FARG PQ 5
	FARG R -1
	Stop
	XDeath: //new
	FARX A 0 A_SpawnDebris("Gore_GreenGib11",0)
	FARX A 0 A_SpawnDebris("Gore_GreenGib11",0)
	FARX A 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,0)
	FARX A 5 A_ChangeFlag("FLOATBOB", 0)
	FARX B 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,2)
	FARX B 5 A_Scream
	FARX C 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,4)
	FARX D 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,6)
	FARX CD 5
	FARX E 0 A_SpawnItem("Gore_GibGeneratorGreen",0,0,0,8)
	FARX E 5 A_NoBlocking
	FARX F 5
	FARX G 5
	FARX HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 0 A_SpawnItemEx ("GreenFartCloud",random(-64,64),random(-64,64),random(-48,48),0,0,0.5,0,160)
	FARX H -1
	Stop
	Ice:
	FARX I 0 A_ChangeFlag("FLOATBOB", 0)
	FARX I 0 A_ChangeFlag("NOGRAVITY", 0)
	FARX I 5 A_FreezeDeath
	FARX I 1 A_FreezeDeathChunks
	Wait
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumGreen",0,0,0,0)
	stop
	Raise: //new - old raise wasn't good for Gore
	FARG R 4 A_ChangeFlag("FLOATBOB", 1)
	FARG QPONMLKJI 4
	goto See
	}
}

actor GreenFartCloud
{
radius 2
height 2
renderstyle Translucent
+NOINTERACTION
Poisondamage 10
states
	{
	Spawn:
	FUGS ABCDE 3
	Fade:
	FUGS ABCDE 3 A_FadeOut (0.05)
	loop
	}
}

actor FartBallFX
{
radius 2
height 2
renderstyle Add
Scale 0.25
+NOINTERACTION
states
	{
	Spawn:
	CSSF DE 4 A_FadeOut (0.2)
	loop
	}
}

ACTOR FartBall
{
Speed 15
Radius 8
Height 8
Damage 5
Projectile
RenderStyle Add
SeeSound "RotWraith/Attack"
DeathSound "RotWraith/Explode"
States
	{
	Spawn:
	CSSF DDDDEEEE 1 Bright A_SpawnItemEx ("FartBallFX", 0, random (-3, 3), random (-3, 3), 0, 0, 0, 0, 160)
	Loop
	Death:
	CSSF F 4 Bright
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("GreenFartCloud", random (-64, 64), random (-64, 64), random (-48, 48), 0, 0, 0.5, 0, 160)
	TNT1 AAAAA 0 A_SpawnItemEx ("GreenFartCloud", random (-80, 80), random (-80, 80), random (-48, 64), 0, 0, 0.5, 0, 160)
	CSSF GHI 4 Bright
	TNT1 AAAAAAAAAA 5 A_Explode((2), 80, 0, 0, 80)
	Stop
	}
}