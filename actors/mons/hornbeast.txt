actor HornBeast 30113
{
//$Category Realm667 Monsters
Tag "Horn Beast"
Game Doom
spawnid 120
gibhealth 35
Health 500
Speed 10
Radius 20
Height 60
Mass 800
PainChance 50
DamageFactor Fire, 0
DamageFactor HellFire, 0
DamageFactor Ice, 1.5
MONSTER
+FLOORCLIP
SeeSound "horn/sight"
ActiveSound "demon/active"
PainSound "demon/pain"
DeathSound "horn/death"
Obituary "%o was burned to crisp by horn beast."
DropItem "BigGas", 96
DropItem "ShellBox", 32
DropItem "RegenSphere", 16
BloodColor "darkorange1"
MaxStepHeight 32
MaxDropOffHeight 32
States
	{
	Spawn:
	HBST A 0 A_Jump (81,2)
	HBST A 10 Bright A_Look
	Loop
	HSBT A 0 A_CustomMissile ("HBeastSmoke",64,0,0)
	Loop
	See:
	HBST A 0 A_Jump (81,9)
	HBST AABBCCDD 3 Bright A_Chase
	Loop
	HSBT A 0 A_CustomMissile ("HBeastSmoke",64,0,0)
	Loop
	Missile:
	HBST E 10 Bright A_FaceTarget
	HBST E 0 A_CustomMissile ("HBeastShot",32,20,0)
	HBST E 0 A_CustomMissile ("HBeastShot",32,-20,0)
	HBST E 5 Bright A_FaceTarget
	Goto See
	Pain:
	HBST F 3 Bright
	HBST F 3 Bright A_Pain
	Goto See
	Death:
	HBST G 8 Bright A_Scream
	HBST H 7 Bright A_PlaySound ("horn/shotx")
	HBST I 6 Bright
	HBST JK 5 Bright
	HBST LMNO 4
	HBST P -1
	Stop
	XDeath:
	HBST G 0 Bright A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,0)
	HBST G 8 Bright A_Scream
	HBST H 0 Bright A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,1)
	HBST H 7 Bright A_PlaySound ("horn/shotx")
	HBST I 0 Bright A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,2)
	HBST I 6 Bright
	HBST J 0 Bright A_SpawnItem("Gore_GibGeneratorOnyx",0,0,0,3)
	HBST K 0 Bright A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,4)
	HBST JK 5 Bright
	HBST L 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,5)
	HBST LMNO 4
	HBST P 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	HBST P 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	HBST P 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	HBST P 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	HBST P 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	HBST P -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedMediumOnyx",0,0,0,0)
	stop
	Raise:
	HBST PONMLKJIHG 2
	goto See
	}
}

actor HBeastSmoke
{
Radius 0
Height 0
Speed 0
Alpha 0.3
Scale 0.55
Projectile
RenderStyle Add
States
	{
	Spawn:
	BISH QRSTUVW 3
	BISH W 3 A_FadeOut (0.50)
	Stop
	}
}

actor HBeastShot
{
Radius 2
Height 3
Speed 20
Alpha 0.8
Projectile
RenderStyle Add
+FLOORHUGGER
-NOBLOCKMAP
SeeSound "horn/attack"
DeathSound "horn/shotx"
States
	{
	Spawn:
	CFFX NOP 10 Bright
	Death:
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-5)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,5)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-10)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,10)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-20)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,20)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-30)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,30)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-40)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,40)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-50)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,50)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-60)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,60)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-70)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,70)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-80)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,80)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-90)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,90)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,-100)
	CFFX A 0 A_CustomMissile ("HBeastFlame",1,0,100)
	CFCF Q 0 A_Scream
	CFCF Q 1 Bright A_Explode (128,128,0,1)
	CFCF QRSTUVXYZ 3 Bright
	Stop
	}
}

actor HBeastFlame
{
Radius 5
Height 4
Speed 20
Alpha 0.8
Damage 3
Decal DoomImpScorch
Projectile
RenderStyle Add
+FLOORHUGGER
SeeSound "imp/shotx"
States
	{
	Spawn:
	CFCF ABCDEFGHIJKLMNOP 2 Bright
	Death:
	TNT1 A 1
	Stop
	}
}