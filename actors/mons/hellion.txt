actor Hellion 30132
{
//$Category Realm667 Monsters
Tag "Hellion"
Game Doom
spawnid 115
gibhealth 35
obituary "A Hellion burned %o was to a crisp."
health 70
radius 20
height 56
mass 120
speed 10
painchance 24
seesound "monster/hlnsit"
painsound "monster/hlnpai"
deathsound "monster/hlndth"
activesound "monster/hlnact"
MONSTER
+FLOORCLIP
DropItem "ArmorShard", 16
DropItem "SmallPotionsSpawner", 48
bloodcolor "darkorange1"
DamageFactor Fire, 0.5
DamageFactor HellFire, 0.5
MaxStepHeight 32
MaxDropOffHeight 32
states
	{
	Spawn:
	HELN AB 10 A_Look
	loop
	See:
	HELN AABBCCDDEEFF 2 A_Chase
	loop
	Missile:
	HELN G 0 A_VileStart
	HELN GHIJK 6 Bright A_FaceTarget
	HELN L 6 Bright A_CustomMissile("HellionBall",32,0,0,0,0)
	HELN L 2
	goto See
	Pain:
	HELN M 2
	HELN M 2 A_Pain
	goto See
	Death:
	HELN N 6
	HELN O 6 A_Scream
	HELN PQR 6
	HELN S 6 A_NoBlocking
	HELN T -1
	stop
	XDeath: //new
	HELN U 0 A_SpawnDebris("NashGore_Gib11",0)
	HELN U 0 A_SpawnDebris("Gore_OrangeGib11",0)
	HELN U 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,0)
	HELN U 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)
	HELN U 5
	HELN V 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,2)
	HELN V 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,2)
	HELN V 5 A_XScream
	HELN W 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,4)
	HELN W 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,4)
	HELN W 5
	HELN X 0 A_SpawnItem("Gore_GibGeneratorOrange",0,0,0,6)
	HELN X 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,6)
	HELN X 5 A_NoBlocking
	HELN YZ 5
	HELN "[\]" 5
	HELN ] -1
	stop
	Burn: //new
	BURN A 3 Bright
	BURN B 3 Bright A_Scream
	BURN C 3 Bright A_Wander
	BURN D 3 Bright A_NoBlocking
	BURN EFGHIJKLMNOP 5 Bright
	BURN Q 3 Bright
	BURN RSTU 7 Bright
	BURN V 4 A_CustomMissile ("HBeastSmoke",32,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",36,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",40,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",44,0,0)
	BURN V 4 A_CustomMissile ("HBeastSmoke",48,0,0)
	BURN V -1
	Stop
	Crush:
	TNT1 A 1 A_SpawnItem("NashGore_CrushedSmall",0,0,0,0)
	stop
	Raise:
	HELN TSRQPON 6
	goto See
	}
}

ACTOR HellionBall : CacodemonBall
{
Damage 8
Speed 15
Alpha 0.80
DamageType fire
+THRUGHOST
+FORCEXYBILLBOARD
SeeSound "Monster/hlnatk"
DeathSound "Monster/hlnexp"
states
	{
	Spawn:
	HLBL AB 3 bright A_SpawnItemEx("HellionPuff",0,0,0,0,0,0)
	loop
	Death:
	HLBL JKLMN 3 bright
	stop
	}
}

ACTOR HellionPuff
{
Radius 3
Height 3
RENDERSTYLE ADD
Alpha 0.67
+NOGRAVITY
+NOBLOCKMAP
+DONTSPLASH
+FORCEXYBILLBOARD
States
	{
	Spawn:
	NULL A 3 Bright
	HLBL CDEFGHI 3 BRIGHT
	Stop
	}
}