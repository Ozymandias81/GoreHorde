//TheBadHustlex Grenade Launcher - sprites from Demon Eclipse by Eriance
Actor TBHGrenadeLauncher : Weapon 30122
{
//$Category New Weapons
Tag "Hustling Grenade Launcher"
Game Doom
Radius 20
Height 16
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
+WEAPON.STAFF2_KICKBACK
Weapon.AmmoUse 1
Weapon.AmmoUse2 1
Weapon.AmmoGive 2
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoType2 "RocketAmmo"
Weapon.SlotNumber 5
Inventory.PickupMessage "You got an hustling grenade launcher!"
Obituary "%o caught %k's grenade."
States
	{
	Spawn:
	HGRL A -1
	Stop
	Ready:
	HGLG A 1 A_WeaponReady
	Loop
	Deselect:
	HGLG A 1 A_Lower
	Loop
	Select:
	HGLG A 1 A_Raise
	Loop
	Fire:
	HGLG D 2
	TNT1 A 0 A_SetBlend("Orange",0.040,4)
	TNT1 A 0 A_ZoomFactor(0.96)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG E 3 A_FireCustomMissile("TBHGrenade", 0, 1, 0, 0, 0, 6.328125)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG F 2 A_Zoomfactor(0.97)
	TNT1 A 0 A_Setpitch(pitch-0.2)
	HGLG G 2 A_Zoomfactor(0.98)
	HGLG H 2 A_Zoomfactor(0.99)
	HGLG CBA 2 A_Zoomfactor(1)
	HGLG A 0 A_ReFire
	Goto Ready
	AltFire: //new
	HGLG D 5
	TNT1 A 0 A_SetBlend("Orange",0.040,4)
	TNT1 A 0 A_ZoomFactor(0.96)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG E 6 A_FireCustomMissile("TBHGrenade", 0, 1, 0, 0, 0, 6.328125)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG F 5 A_Zoomfactor(0.97)
	TNT1 A 0 A_SetBlend("Orange",0.040,4)
	TNT1 A 0 A_ZoomFactor(0.96)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG E 6 A_FireCustomMissile("TBHGrenade", 0, 1, 0, 0, 0, 6.328125)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG F 5 A_Zoomfactor(0.97)
	TNT1 A 0 A_SetBlend("Orange",0.040,4)
	TNT1 A 0 A_ZoomFactor(0.96)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG E 6 A_FireCustomMissile("TBHGrenade", 0, 1, 0, 0, 0, 6.328125)
	TNT1 A 0 A_Setpitch(pitch-0.5)
	HGLG F 5 A_Zoomfactor(0.97)
	TNT1 A 0 A_Setpitch(pitch-0.2)
	HGLG G 3 A_Zoomfactor(0.98)
	HGLG H 2 A_Zoomfactor(0.99)
	HGLG CBA 2 A_Zoomfactor(1)
	HGLG A 0 A_ReFire
	Goto Ready
	Flash:
	GRLF A 3 Bright A_Light1
	GRLF B 4 Bright
	GRLF C 4 Bright A_Light2
	GRLF D 4 Bright A_Light2
	Goto LightDone
	}
}

Actor TBHGrenade
{
Radius 8
Height 8
Speed 25
Damage 20
Gravity 0.25
Projectile
-NOGRAVITY
+DOOMBOUNCE
+DEHEXPLOSION
+GRENADETRAIL
SeeSound "Weapons/GrBnce"
DeathSound "Weapons/GrenLX"
DamageType Fire
States
	{
	Spawn:
	SGRN A 0 Bright
	SGRN A 0 Bright A_StopSound
	SGRN A 0 Bright A_PlaySound("weapons/stgrshot")
	SGRN A 1 Bright
	Goto Spawn+3
	Death:
	MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
	MISL B 8 Bright A_Explode
	MISL C 6 Bright
	MISL D 4 Bright
	stop
	}
}