Actor RainSpawner : SwitchableDecoration 10008
{
//$Category Special Effects
//$Sprite RNDRA0
Tag "Rain Spawner"
Game Doom
Radius 1
Height 1
+NOCLIP
+NOGRAVITY
+SPAWNCEILING
+CLIENTSIDEONLY
States
	{
	Spawn:
	Active:
	TNT1 A 0 NoDelay A_JumpIf(CallACS("Weather")==0,"Unsighted")
	TNT1 A 0 A_CheckSight("Unsighted")
	TNT1 A 0 A_JumpIf(Args[2] > 0, "NoSound")
	TNT1 A 0 A_JumpIf(Args[3] > 0, "Circle")
	TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7", 1)
	TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, momz, 0, 128, Args[1])
	Loop
	Circle:
	TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, momz, Random(0, 360), 128, Args[1])
	NoSound:
	TNT1 A 0 A_Jumpif(Args[3] > 0, "NoSoundCircle")
	TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), Random(-Args[0], Args[0]), -2, 0, 0, momz, 0, 128, Args[1])
	Loop
	NoSoundCircle:
	TNT1 A 2 A_SpawnItemEx("RainDrop", Random(-Args[0], Args[0]), 0, -2, 0, 0, momz, Random(0, 360), 128, Args[1])
	Loop
	Unsighted:
	TNT1 A 1
	Goto Spawn
	Inactive:
	TNT1 A -1
	Stop
	}
}

Actor RainDrop
{
+MISSILE
-NOGRAVITY
+NOBLOCKMAP
+DONTSPLASH
Height 2
Radius 2
Renderstyle Translucent
Alpha 0.6
Scale 2.1
States
	{
	Spawn:
	RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
	Loop
	Death:
	RNDR BCDEFGH 3 A_FadeOut(0.15)
	Stop
	}
}