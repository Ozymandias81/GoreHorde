Actor AutumnSpawner : SwitchableDecoration 14070
{
//$Category Special Effects
//$Sprite LEF1A0
Tag "Autumn Spawner"
Game Doom
Radius 1
Height 1
+NOCLIP
+NOGRAVITY
+NOTELEFRAG
+NOTELEOTHER
+CLIENTSIDEONLY
States
	{
	Spawn:
	Active:
	TNT1 A 0
	TNT1 A 0 A_CheckSight("Unsighted")
	TNT1 A 4 A_SpawnItemEx("FloatingLeaf", Random(-Args[0],Args[0]), Random(-Args[0],Args[0]), Random(0,Args[1]), Random(Args[2],Args[3]), 0, Random(-Args[2],Args[2]), Random(-4, 4), 128, Args[4])
	Loop
	Unsighted:
	TNT1 A 1
	Goto Spawn
	Inactive:
	TNT1 A 1
	Loop
	}
}
//Decorate base from SnowSpawner
Actor ZAutumnSpawner : SwitchableDecoration 8898
{
//$Category Special Effects
//$Sprite LEF1A0
Tag "Autumn Spawner (Falling)"
Game Doom
Radius 1
Height 1
+CLIENTSIDEONLY
+NOINTERACTION
+SPAWNCEILING
+NOBLOCKMAP
+NOGRAVITY
+NOSECTOR
+NOCLIP
-SOLID
States
	{
	Spawn:
	Active:
	TNT1 A 0 NoDelay
	TNT1 A 0 A_CheckSight("Unsighted")
	TNT1 A 0 A_JumpIf(Args[2] > 0, "Circle")
	TNT1 A 8 A_SpawnItemEx ("FloatingLeaf", Random(-Args[0], -Args[0]), Random(-Args[0], -Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 128, Args[1])
	Loop
	Circle:
	TNT1 A 8 A_SpawnItemEx ("FloatingLeaf", Random(-Args[0], -Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), Random(0, 360), 128, Args[1])
	Loop
	Unsighted:
	TNT1 A 1
	Goto Spawn
	Inactive:
	TNT1 A -1
	Stop
	}
}

Actor FloatingLeaf
{
+MISSILE
+NOGRAVITY
+DONTSPLASH
+NOBLOCKMAP
+WINDTHRUST
+NOTELEFRAG
+NOTELESTOMP
Radius 1
Height 1
RenderStyle Normal
ReactionTime 250
States
	{
	Spawn:
	LEF1 A 0
	LEF1 A 0 A_SetScale(FRandom(0.75, 1.00))
	LEF1 A 0 A_Jump(192, "Spawn1", "Spawn2", "Spawn3")
	Spawn1:
	LEF1 ABCDEFGHI 3 A_CountDown
	Loop
	Spawn2:
	LEF2 ABCDEFGHI 3 A_CountDown
	Loop
	Spawn3:
	LEF3 ABCDEFGHI 3 A_CountDown
	Loop
	Death:
	LEF1 A 4 A_FadeOut(0.06)
	Loop
	}
}