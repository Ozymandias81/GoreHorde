actor BubbleSpawner : SwitchableDecoration 12345
{
//$Category Special Effects
//$Sprite SBUBA0
Tag "Bubbles Spawner"
Game Doom
Radius 2
Height 2
+NOINTERACTION
States
	{
	Spawn:
	Active:
	TNT1 A 0
	TNT1 A 0 A_CheckSight("Unsighted")
	TNT1 A 2 A_SpawnItemEx("Bubble", random (-16, 16), 0, 0, 0, 0, 2, random (0, 360), 0, 128)
	loop
	Unsighted:
	TNT1 A 1
	Goto Spawn
	Inactive:
	TNT1 A 1
	Loop
	}
}

actor Bubble
{
Radius 2
Height 2
Speed 1
Scale 0.05
Alpha 0.25
RenderStyle Add
PROJECTILE
+NOCLIP
+DONTSPLASH
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
States
	{
	Spawn:
	SBUB A 1
	Rise:
	SBUB A 0 A_JumpIf(waterlevel == 0, "Death")
	SBUB A 1 A_Jump (32, "Jiggle")
	loop
	Jiggle:
	SBUB A 0 A_JumpIf(waterlevel == 0, "Death")
	SBUB A 1 A_SetAngle (random (0, 360))
	SBUB A 0 A_Recoil (0.1)
	goto Rise
	Death:
	TNT1 A 1
	stop
	}
}